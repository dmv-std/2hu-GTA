{======================================================================
======================================================================}
{$CLEO .cs}
thread 'Main'

{======================================================================
======================================================================}
{$I main/groupmanager/ModelsOffsets.txt}

{======================================================================
======================================================================}
const
    Gm_State = 0@
    Dummy1 = 8@
    Dummy2 = 9@
    Dummy3 = 10@
    Dummy4 = 11@
    FlankUp = Dummy1
    FlankDown = Dummy2
    // 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@: Team in use
    AxisXPressState = 21@
    AxisX = 22@
    ThSelect = 23@
    CurrSelect = 24@
    
    FLAG_INCREASE=0
    FLAG_DECREASE=1

    FLAG_FALSE=0
    FLAG_TRUE=1

    FLAG_PAD_PRESS = 0
    FLAG_KEY_PRESS = 1
    
    PAD_LEFT_RIGHT = 0
    PAD_UP_DOWN = 1
    
    LOG_FILE_PATH = "cleo/2hugta_main.log"
    SAVEFILE_PATH = "cleo\2hugta.sav"
    TEMPFILE_PATH = "cleo\TEMP"
end

{======================================================================
======================================================================}
:Main
wait 0
if 0AAB:   file_exists "cleo/__2hugta__"
then 0AB1: @ReadCityFromFile 0 ret 10@ 11@
else 0A93: end_custom_thread
end

if 10@s == 'LibCity'
then 0AB1: @IIIFade 2 0 0
else fade 0 0
end

0A94: start_custom_mission "missions/CmInitialize"

if 0AAB:   file_exists SAVEFILE_PATH
then
    0AB1: @GetPlayerCharacterData 1 create_log_in_file 0 store_player_character_data_in Dummy1 ThSelect
    0085: CurrSelect = ThSelect // (int)
else
    0AB1: @Debug_ChooseGroup 0 ret Dummy1
    ThSelect=0
    CurrSelect=0
end

0AB1: @InitializeTeamInUse 1 Dummy1
0AB1: @GetTeamInUse 0 ret 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@
0AB1: @CopyTeamModels 0

0AB1: @ChangePlayerSkin 2 13@(ThSelect,7i) ThSelect // TODO: data 13@(ThSelect,7i) looks unnecessary

0A9A: Dummy1 = openfile LOG_FILE_PATH mode "wb+"
0AD9: write_formatted_text "Team in use: %.2d %.2d %.2d %.2d %.2d %.2d %.2d %.2d, selected: %d%c%c" in_file Dummy1 vars 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ ThSelect 0xd 0xa
0A9B: closefile Dummy1

wait 1500
0AB1: @GetLoadedCity 0 ret 10@ 11@
if 10@s == 'LibCity'
then 0AB1: @IIIFade 2 1 2500
else fade 1 2500
end

{======================================================================
Main loop
======================================================================}
while true
wait 0
    gosub @JobTriggerer
    gosub @SaveMachine
    gosub @GroupManager_loop
end

{======================================================================
======================================================================}
{$I common/SearchIntoTemp.txt}
{$I common/Utilities.txt}
{$I common/DrawTexts.txt}
{$I common/Math.txt}
{$I common/GetButtonFlanks.txt}
{$I common/FloatToInt.txt}

{$I main/groupmanager/GroupManager.txt}
{$I main/groupmanager/DebugChooseGroup.txt}
{$I main/groupmanager/ModelsList.txt}
{$I main/groupmanager/ModelManager.txt}
{$I main/groupmanager/StatScreen.txt}

{$I main/JobTriggerer.txt}
{$I main/SaveMachine.txt}

{======================================================================
======================================================================}
:LoadedCity
hex
FFFFFFFF FFFFFFFF
end

:TeamAvailabilityFlag
hex
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 01 00 00 00 00 00
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 00 01 00 00 00 00
    01 00 00 00 00 00 00
    01 01 01 00 00 00 00
    00 00 01 00 00 00 00
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 00 00 00 00 00 01
    00 00 00 00 00 00 01
end
    
:TeamInUse
hex
    FF FF FF FF FF FF FF FF
end

{======================================================================
======================================================================}
:StoreLoadedCity
0AC6: 2@ = label @LoadedCity offset
0A8C: write_memory 2@ size 4 value 0@ virtual_protect 0
2@+=4
0A8C: write_memory 2@ size 4 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:GetLoadedCity
0AC6: 2@ = label @LoadedCity offset
0A8D: 0@ = read_memory 2@ size 4 virtual_protect 0
2@+=4
0A8D: 1@ = read_memory 2@ size 4 virtual_protect 0
0AB2: ret 2 0@ 1@

{======================================================================
======================================================================}
:ReadCityFromFile
0A9A: 0@ = openfile "cleo/__2hugta__" mode "rb"  // IF and SET
0ADA: 1@ = scan_file 0@ format "%s" 2@s //IF and SET
0A9B: closefile 0@
if 1@ <> 1
then
    059A:  return_false
    0ACC: show_text_lowpriority "~r~ERROR: no city information found. I don't know which city i am." time 5000
else
    0485:  return_true
    0AB1: @StoreLoadedCity 2 2@ 3@
end
0AB2: ret 2 2@ 3@

