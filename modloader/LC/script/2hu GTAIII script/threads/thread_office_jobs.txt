:OFFICE_JOBS
thread 'OFFJ'

{==================================================
MAIN LOOP
==================================================}
:OFFICE_LOOP
wait 0 ms
gosub @OFFICE
jump @OFFICE_LOOP
end_thread

{==================================================
SUBROUTINES
==================================================}
:OFFICE
if 10@ == 0
then
    if 00FE:   actor $PLAYER_ACTOR 0 900.0831 -284.6876 5.1457 radius 60.0 60.0 2.0
    then
        if 00FF:   actor $PLAYER_ACTOR 1 900.0831 -284.6876 5.1457 radius 2.0 2.0 4.0
        then
            if $ONMISSION == 0
            then
                if and
                $knowledge >= 250
                $l_exp >= 60
                then
                    if $energy >= 120
                    then
                        03E5: text_box 'T_OF01' // Pulsa el ~h~boton ~k~~VEHICLE_ENTER_EXIT~~w~ para hacer trabajos de oficina (4 horas / ~b~-120 energia~w~).
                        10@=1
                    else
                        03E5: text_box 'T_OF02' // Estas muy agotada como para realizar trabajos de oficina...
                        10@=3
                    end
                else
                    03E5: text_box 'T_OF03' // Se solicita persona con ~g~experiencia~w~ y ~p~conocimientos~w~ para desempeñar trabajos de oficina.
                    10@=3
                end
            else
                03E5: text_box 'T_OF04' // No puedes hacer trabajos de oficina mientras te encuentres en una mision.
                10@=3
            end
        end
    end
end

if 10@ == 1
then
    if 00E1:   pad 0 key_pressed K_ENTER
    then 10@=2
        01B4: set_player $PLAYER_CHAR frozen_state 0
    end
    if 80FF:   not actor $PLAYER_ACTOR 0 900.0831 -284.6876 5.1457 radius 2.0 2.0 4.0
    then 10@=0
    end
end

if 10@ == 2
then
    if 80E1:   not pad 0 key_pressed K_ENTER
    then 10@=4
    end
end

if 10@ == 3
then
    if 80FF:   not actor $PLAYER_ACTOR 0 900.0831 -284.6876 5.1457 radius 2.0 2.0 4.0
    then 10@=0
        03E6: remove_text_box
    end
end

if 10@ == 4
then
    03E6: remove_text_box 
    $energy-=120
    0169: set_fade_color 0 0 0
    016A: fade 0 1000 ms
    wait 1000
    0055: put_player $PLAYER_CHAR at 900.4158 -280.5895 4.9709
    0171: set_player $PLAYER_CHAR z_angle_to 0.0 
    0373: set_camera_directly_behind_player
    02EB: restore_camera_with_jumpcut
    wait 1000
    016A: fade 1 1000 ms
    0209: 4@ = random_int 25 33
    0084: 5@ = $knowledge // integer values and handles 
    0209: 6@ = random_int 2 5
    0070: 5@ /= 6@ // integer values 
    0058: 4@ += 5@ // integer values 
    0084: 5@ = $l_exp // integer values and handles 
    0209: 6@ = random_int 4 12
    0070: 5@ /= 6@ // integer values 
    0058: 4@ += 5@ // integer values 
    01E4: text_1number_lowpriority 'T_OF05' 4@ 4000 ms 1 // Haz trabajado por 4 horas en la oficina. Haz ganado un total de ~b~$~1~~h~.
    0109: player $PLAYER_CHAR money += 4@ 
    gosub @M_OFFICE_EXPERIENCE
    01B4: set_player $PLAYER_CHAR frozen_state 1 
    10@=3
end
return

:M_OFFICE_EXPERIENCE
0008: $l_exp += 1 // integer values 
create_thread @DRAW_STATS ID_LABORAL_EXP
0209: 4@ = random_int 1 3 
0058: $m_office_exp += 4@ // integer values 
create_thread @DRAW_STATS ID_OFFICE_EXP
return

