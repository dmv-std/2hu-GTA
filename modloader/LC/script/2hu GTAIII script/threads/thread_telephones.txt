:TELEPHONES
thread 'PHONE'
{
10@: state variable
4@: menu level currently displayed
5@: option selected from menu first level
6@: option selected from menu second level
12@: shown text
}

{==================================================
MAIN LOOP
==================================================}
:PHONE_LOOP
wait 0 ms

if 10@ == 0
then
    if 00FE:   actor $PLAYER_ACTOR 0 939.5073 -230.3825 4.9411 radius 40.0 40.0 2.0
    then
        if and
        00FF:   actor $PLAYER_ACTOR 1 939.5073 -230.3825 4.9411 radius 2.0 2.0 2.0
        02A0:   actor $PLAYER_ACTOR stopped 
        then 10@=1
             03E5: text_box 'T_PH01' // Pulsa el ~h~boton ~k~~VEHICLE_ENTER_EXIT~~w~ para usar el Telefono Publico.
        end
    end
    if $G_MARKER_ENABLED == 1
    then
        if 00FE:   actor $PLAYER_ACTOR 0 $G_MARKER_X $G_MARKER_Y $G_MARKER_Z radius 20.0 20.0 2.0
        then
            0164: disable_marker $G_MARKER
            $G_MARKER_ENABLED=0
            00BB: show_text_lowpriority GXT 'T_PH52' time 5000 flag 1  // Haz llegado a tu ~b~destino~w~.
        end
    end
    if and
      $ONMISSION == 1
      $G_MARKER_ENABLED == 1
    then
        0164: disable_marker $G_MARKER
        $G_MARKER_ENABLED=0
    end
end

if 10@ == 1
then
    if 00E1:   pad 0 key_pressed K_ENTER
    then
        01B4: set_player $PLAYER_CHAR frozen_state 0
        10@=2
    end
    if 80FF:   not actor $PLAYER_ACTOR 0 939.5073 -230.3825 4.9411 radius 2.0 2.0 2.0
    then 03E6: remove_text_box
         10@=0
    end
end

if 10@ == 2
then
    if 80E1:   not pad 0 key_pressed K_ENTER
    then
        gosub @SHOW_PHONE_MENU
        10@=99
    end
end

if 10@ == 3
then
    if 80FF:   not actor $PLAYER_ACTOR 0 939.5073 -230.3825 4.9411 radius 2.0 2.0 2.0
    then 10@=0
    end
end

// Estado de finalizacion de maquina
if 10@ == 98
then
    01B4: set_player $PLAYER_CHAR frozen_state 1
    10@=3
    4@=0
    5@=0
    6@=0 
    12@=0
end

if 10@ == 99
then
    03E5: text_box 'T_PH99' // ~h~< / >:~w~ Explorar opciones ~h~~k~~PED_JUMPING~:~w~ Elegir ~h~~k~~VEHICLE_ENTER_EXIT~~h~ Cancelar.
    10@=100
end

// Estado de reconocimiento de botones (reaccionar apropiadamente al boton pulsado)
if 10@ == 100
then
    if 00E1:   pad 0 key_pressed K_LEFT
    then 10@=200
    end
    
    if 00E1:   pad 0 key_pressed K_RIGHT
    then 10@=300
    end
    
    if 00E1:   pad 0 key_pressed K_JUMP
    then 10@=400
    end
    
    if 00E1:   pad 0 key_pressed K_ENTER
    then 10@=500
    end
end

// Estados 200, 300, 400 y 500: esperar a que el boton que ha sido pulsado sea liberado
if 10@ == 200
then
    if 80E1:   not pad 0 key_pressed K_LEFT
    then 10@=210
    end
end

if 10@ == 300
then
    if 80E1:   not pad 0 key_pressed K_RIGHT
    then 10@=310
    end
end

if 10@ == 400
then
    if 80E1:   not pad 0 key_pressed K_JUMP
    then 10@=410
    end
end

if 10@ == 500
then
    if 80E1:   not pad 0 key_pressed K_ENTER
    then 10@=510
    end
end

if 10@ == 210 // ha sido presionado LEFT
then
    10@=99 // volver al estado de reconocimiento de pulsaciones de botones
    if 4@ == 0 // estoy en el primer nivel de opciones desplegadas
    then gosub @DEC_PHONE_MENU
    end
    if 4@ == 1 // estoy en el segundo nivel de opciones desplegadas
    then
        if 5@ == 0 // Fue elegida la opcion 0 (Telefono publico)
        then gosub @DEC_PHONE_CALLS
        end
        if 5@ == 1 // Fue elegida la opcion 1 (Paginas amarillas)
        then gosub @DEC_YELLOW_PAGES
        end
    end
    if 4@ == 2 // estoy en el tercer nivel de opciones desplegadas
    then
        if 5@ == 1 // Fue elegida la opcion 1 (Paginas amarillas)
        then
            if 6@ == 0 // Fue elegida la opcion 0 (actividades)
            then gosub @DEC_ACTIVITIES
            end
            if 6@ == 1 // Fue elegida la opcion 1 (servicios publicos)
            then gosub @DEC_PUBLIC_SERV
            end
            if 6@ == 2 // Fue elegida la opcion 2 (empleos)
            then gosub @DEC_JOBS
            end
        end
    end
end

if 10@ == 310 // ha sido presionado RIGHT
then
    10@=99 // volver al estado de reconocimiento de pulsaciones de botones
    if 4@ == 0 // estoy en el primer nivel de opciones desplegadas
    then gosub @INC_PHONE_MENU
    end
    if 4@ == 1 // estoy en el segundo nivel de opciones desplegadas
    then
        if 5@ == 0 // Fue elegida la opcion 0 (Telefono publico)
        then gosub @INC_PHONE_CALLS
        end
        if 5@ == 1 // Fue elegida la opcion 1 (Paginas amarillas)
        then gosub @INC_YELLOW_PAGES
        end
    end
    if 4@ == 2 // estoy en el tercer nivel de opciones desplegadas
    then
        if 5@ == 1 // Fue elegida la opcion 1 (Paginas amarillas)
        then
            if 6@ == 0 // Fue elegida la opcion 0 (actividades)
            then gosub @INC_ACTIVITIES
            end
            if 6@ == 1 // Fue elegida la opcion 1 (servicios publicos)
            then gosub @INC_PUBLIC_SERV
            end
            if 6@ == 2 // Fue elegida la opcion 2 (empleos)
            then gosub @INC_JOBS
            end
        end
    end
end

if 10@ == 410 // ha sido presionado JUMP
then
    10@=99 // volver al estado de reconocimiento de pulsaciones de botones
    if 4@ == 0 // estoy en el primer nivel de opciones desplegadas
    then
        4@=1
        if 12@ == 0 // Telefono publico
        then
            gosub @SHOW_PHONE_CALLS
            5@=0
        end
        if 12@ == 1 // Paginas amarillas
        then
            gosub @SHOW_YELLOW_PAGES
            5@=1
        end
    else
        if 4@ == 1 // estoy en el segundo nivel de opciones desplegadas
        then
            if 5@ == 0 // Sub-menu: telefono publico
            then
                if 12@ == 0 // realizar llamada
                then
                    gosub @CALL_2HU_PARTNER
                    10@=98 // Finalizar maquina
                end
            end
            if 5@ == 1 // Sub-menu: paginas amarillas
            then
                4@=2
                if 12@ == 0 // actividades
                then
                    gosub @SHOW_ACTIVITIES
                    6@=0
                    4@=2
                end
                if 12@ == 1 // servicios publicos
                then
                    gosub @SHOW_PUBLIC_SERV
                    6@=1
                end
                if 12@ == 2 // empleos
                then
                    gosub @SHOW_JOBS
                    6@=2
                end
                if 12@ == 3 // cancelar busqueda
                then
                    10@=98 // Finalizar maquina
                    03E6: remove_text_box 
                    00BE: text_clear_all 
                    if $G_MARKER_ENABLED == 1
                    then 0164: disable_marker $G_MARKER
                         $G_MARKER_ENABLED=0
                         00BB: text_lowpriority 'T_PH53' 5000 ms 1 // El objetivo ha sido removido del mapa.
                    else 00BB: text_lowpriority 'T_PH50' 5000 ms 1 // No tienes ningun objetivo trazado en el mapa.
                    end
                end
            end
        else
            if 4@ == 2 // estoy en el tercer nivel de opciones desplegadas
            then
                if 5@ == 1 // Sub-menu: paginas amarillas
                then
                    10@=98 // Finalizar maquina
                    03E6: remove_text_box 
                    00BE: text_clear_all 
                    0164: disable_marker $G_MARKER 
                    00BB: text_lowpriority 'T_PH51' 5000 ms 1 // Haz copiado el ~b~objetivo~w~ en el mapa.
                    $G_MARKER_ENABLED=1
                    if 6@ == 0 // sub-sub-menu: actividades
                    then
                        if 12@ == 0 // entrenar
                        then gosub @STRENGH_MARKER
                        end
                        if 12@ == 1 // estudiar
                        then gosub @KNOWLEDGE_MARKER
                        end
                        if 12@ == 2 // bares
                        then gosub @DRINK_MARKER
                        end
                        if 12@ == 3 // mercados
                        then gosub @MARKETPLACE_MARKER
                        end
                        if 12@ == 4 // tiendas de ropa
                        then gosub @CLOTH_MARKER
                        end
                        018A: $G_MARKER = create_checkpoint_at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                    end
                    if 6@ == 1 // sub-sub-menu: servicios publicos
                    then
                        if 12@ == 0 // banco
                        then gosub @BANK_MARKER
                            018A: $G_MARKER = create_checkpoint_at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                        end
                        if 12@ == 1 // tren subterraneo
                        then gosub @SUBWAY_MARKER
                            018A: $G_MARKER = create_checkpoint_at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                        end
                        if 12@ == 2 // pay'n'spray
                        then gosub @PAYNSPRAY_MARKER
                            02A8: $G_MARKER = create_marker I_PAY_N_SPRAY at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                        end
                        if 12@ == 3 // ammunation
                        then gosub @AMMUNATION_MARKER
                            02A8: $G_MARKER = create_marker I_AMMU at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                        end
                    end
                    if 6@ == 2 // sub-sub-menu: empleos
                    then
                        if 12@ == 0 // paramedico
                        then gosub @AMBULAN_MARKER
                        end
                        if 12@ == 1 // taxi
                        then gosub @TAXI_MARKER
                        end
                        if 12@ == 2 // chofer publico
                        then gosub @BUS_MARKER
                        end
                        if 12@ == 3 // recolector de basura
                        then gosub @TRASHMASTER_MARKER
                        end
                        if 12@ == 4 // heladero
                        then gosub @MRWHOOPER_MARKER
                        end
                        if 12@ == 5 // bombero
                        then gosub @FIREFIGHTER_MARKER
                        end
                        if 12@ == 6 // policia
                        then gosub @POLICE_MARKER
                        end
                        if 12@ == 7 // distribuidor
                        then gosub @DELIVERER1_MARKER
                        end
                        if 12@ == 8 // distribuidor por mar
                        then gosub @DELIVERER2_MARKER
                        end
                        if 12@ == 9 // entrega de paquetes
                        then gosub @DELIVERER3_MARKER
                        end
                        if 12@ == 10 // oficinista
                        then gosub @OFFICE_MARKER
                        end
                        018A: $G_MARKER = create_checkpoint_at $G_MARKER_X $G_MARKER_Y $G_MARKER_Z
                    end
                end
            end
        end
    end
end

if 10@ == 510 // ha sido presionado ENTER
then
    10@=99 // volver al estado de reconocimiento de pulsaciones de botones
    if 4@ == 0 // estoy en el primer nivel de opciones desplegadas
    then
        03E6: remove_text_box
        00BE: text_clear_all 
        10@=98 // Finalizar maquina
    end
    if 4@ == 1 // estoy en el segundo nivel de opciones desplegadas
    then 4@=0
        03D5: remove_text 'T_PHC01' 
        03D5: remove_text 'T_PHC02' 
    end
    if 4@ == 2 // estoy en el tercer nivel de opciones desplegadas
    then 4@=1
        03D5: remove_text 'T_PHC01' 
        03D5: remove_text 'T_PHC02' 
    end
end
jump @PHONE_LOOP

{==================================================
SUBROUTINES
==================================================}

:LOAD_REMI_STATS
// Saving Flan stats
0226: $FLAN_HEALTH = actor $PLAYER_ACTOR health 
0084: $FLAN_ENERGY = $energy // integer values and handles 
0084: $FLAN_MAX_ENERGY = $max_energy // integer values and handles 
0084: $FLAN_STRENGH = $strengh // integer values and handles 
0084: $FLAN_CHARM = $charm // integer values and handles 
0084: $FLAN_KNOWLEDGE = $knowledge // integer values and handles 
0084: $FLAN_REPUTATION = $reputation // integer values and handles 
0084: $FLAN_RESPECT = $respect // integer values and handles 
0084: $FLAN_DRIVING_SKILL = $driving_skill // integer values and handles 
0084: $FLAN_SELLING_SKILL = $selling_skill // integer values and handles 
0084: $FLAN_WEAPON_SKILL = $weapon_skill // integer values and handles 
0084: $FLAN_L_EXP = $l_exp // integer values and handles 
0084: $FLAN_TAXI_EXP = $m_taxi_exp // integer values and handles 
0084: $FLAN_CHOFFEUR_EXP = $m_choffeur_exp // integer values and handles 
0084: $FLAN_DELIV1_EXP = $m_deliv1_exp // integer values and handles 
0084: $FLAN_DELIV2_EXP = $m_deliv2_exp // integer values and handles 
0084: $FLAN_OFFICE_EXP = $m_office_exp // integer values and handles 
0084: $FLAN_AMBULAN_EXP = $m_ambulan_exp // integer values and handles 
0084: $FLAN_FIREFIGHTER_EXP = $m_firefighter_exp // integer values and handles 
0084: $FLAN_COP_EXP = $cop_exp // integer values and handles 
// Loading Remi stats
0222: set_player $PLAYER_CHAR health_to $REMI_HEALTH 
0084: $energy = $REMI_ENERGY // integer values and handles 
0084: $max_energy = $REMI_MAX_ENERGY // integer values and handles 
0084: $strengh = $REMI_STRENGH // integer values and handles 
0084: $charm = $REMI_CHARM // integer values and handles 
0084: $knowledge = $REMI_KNOWLEDGE // integer values and handles 
0084: $reputation = $REMI_REPUTATION // integer values and handles 
0084: $respect = $REMI_RESPECT // integer values and handles 
0084: $driving_skill = $REMI_DRIVING_SKILL // integer values and handles 
0084: $selling_skill = $REMI_SELLING_SKILL // integer values and handles 
0084: $weapon_skill = $REMI_WEAPON_SKILL // integer values and handles 
0084: $l_exp = $REMI_L_EXP // integer values and handles 
0084: $m_taxi_exp = $REMI_TAXI_EXP // integer values and handles 
0084: $m_choffeur_exp = $REMI_CHOFFEUR_EXP // integer values and handles 
0084: $m_deliv1_exp = $REMI_DELIV1_EXP // integer values and handles 
0084: $m_deliv2_exp = $REMI_DELIV2_EXP // integer values and handles 
0084: $m_office_exp = $REMI_OFFICE_EXP // integer values and handles 
0084: $m_ambulan_exp = $REMI_AMBULAN_EXP // integer values and handles 
0084: $m_firefighter_exp = $REMI_FIREFIGHTER_EXP // integer values and handles 
0084: $cop_exp = $REMI_COP_EXP // integer values and handles 
return

:LOAD_FLAN_STATS
// Saving Remi stats
0226: $REMI_HEALTH = actor $PLAYER_ACTOR health 
0084: $REMI_ENERGY = $energy // integer values and handles 
0084: $REMI_MAX_ENERGY = $max_energy // integer values and handles 
0084: $REMI_STRENGH = $strengh // integer values and handles 
0084: $REMI_CHARM = $charm // integer values and handles 
0084: $REMI_KNOWLEDGE = $knowledge // integer values and handles 
0084: $REMI_REPUTATION = $reputation // integer values and handles 
0084: $REMI_RESPECT = $respect // integer values and handles 
0084: $REMI_DRIVING_SKILL = $driving_skill // integer values and handles 
0084: $REMI_SELLING_SKILL = $selling_skill // integer values and handles 
0084: $REMI_WEAPON_SKILL = $weapon_skill // integer values and handles 
0084: $REMI_L_EXP = $l_exp // integer values and handles 
0084: $REMI_TAXI_EXP = $m_taxi_exp // integer values and handles 
0084: $REMI_CHOFFEUR_EXP = $m_choffeur_exp // integer values and handles 
0084: $REMI_DELIV1_EXP = $m_deliv1_exp // integer values and handles 
0084: $REMI_DELIV2_EXP = $m_deliv2_exp // integer values and handles 
0084: $REMI_OFFICE_EXP = $m_office_exp // integer values and handles 
0084: $REMI_AMBULAN_EXP = $m_ambulan_exp // integer values and handles 
0084: $REMI_FIREFIGHTER_EXP = $m_firefighter_exp // integer values and handles 
0084: $REMI_COP_EXP = $cop_exp // integer values and handles 
// Loading Flan stats
0222: set_player $PLAYER_CHAR health_to $FLAN_HEALTH 
0084: $energy = $FLAN_ENERGY // integer values and handles 
0084: $max_energy = $FLAN_MAX_ENERGY // integer values and handles 
0084: $strengh = $FLAN_STRENGH // integer values and handles 
0084: $charm = $FLAN_CHARM // integer values and handles 
0084: $knowledge = $FLAN_KNOWLEDGE // integer values and handles 
0084: $reputation = $FLAN_REPUTATION // integer values and handles 
0084: $respect = $FLAN_RESPECT // integer values and handles 
0084: $driving_skill = $FLAN_DRIVING_SKILL // integer values and handles 
0084: $selling_skill = $FLAN_SELLING_SKILL // integer values and handles 
0084: $weapon_skill = $FLAN_WEAPON_SKILL // integer values and handles 
0084: $l_exp = $FLAN_L_EXP // integer values and handles 
0084: $m_taxi_exp = $FLAN_TAXI_EXP // integer values and handles 
0084: $m_choffeur_exp = $FLAN_CHOFFEUR_EXP // integer values and handles 
0084: $m_deliv1_exp = $FLAN_DELIV1_EXP // integer values and handles 
0084: $m_deliv2_exp = $FLAN_DELIV2_EXP // integer values and handles 
0084: $m_office_exp = $FLAN_OFFICE_EXP // integer values and handles 
0084: $m_ambulan_exp = $FLAN_AMBULAN_EXP // integer values and handles 
0084: $m_firefighter_exp = $FLAN_FIREFIGHTER_EXP // integer values and handles 
0084: $cop_exp = $FLAN_COP_EXP // integer values and handles 
return

:SWITCH_2HU
if $TOUHOU == C_REMI
then 0352: set_actor $PLAYER_ACTOR skin_to 'FLAN'
else 0352: set_actor $PLAYER_ACTOR skin_to 'REMILIA' 
end
while 8248:   not model #NULL available 
wait 0 
end
0353: refresh_actor $PLAYER_ACTOR
return

:CALL_2HU_PARTNER
03E6: remove_text_box
00BE: text_clear_all 
if $TOUHOU == C_REMI
then 00BB: text_lowpriority 'T_PHFL' 3000 ms 1 // Llamando a Flandre...
else 00BB: text_lowpriority 'T_PHRM' 3000 ms 1 // Llamando a Remilia...
end
wait 3600 ms
01B4: set_player $PLAYER_CHAR frozen_state 0
0169: set_fade_color 0 0 0
fade 0 1000
wait 1000
gosub @SWITCH_2HU
if $TOUHOU == C_REMI
then
    $TOUHOU=C_FLAN
    gosub @LOAD_FLAN_STATS
    0055: put_player $PLAYER_CHAR at 966.9354 -140.2126 4.9788
    0171: set_player $PLAYER_CHAR z_angle_to 180.0
else
    $TOUHOU=C_REMI
    gosub @LOAD_REMI_STATS
    0055: put_player $PLAYER_CHAR at 1460.8013 -190.731 55.4698
    0171: set_player $PLAYER_CHAR z_angle_to 85.0
end
0373: set_camera_directly_behind_player
02EB: restore_camera_with_jumpcut 
wait 2000
fade 1 2000
return

// ---------------------------------------------------
:DEC_PHONE_MENU
:INC_PHONE_MENU
if 12@==0
then 12@=1
else 12@=0
end
:SHOW_PHONE_MENU
if 12@ == 0
then 00BA: text_styled 'T_PH03' 6000 ms 4 // mostrar texto 'Telefono Publico'
end
if 12@ == 1
then 00BA: text_styled 'T_PH04' 6000 ms 4 // mostrar texto 'Paginas amarillas'
end
return

// ---------------------------------------------------
:DEC_PHONE_CALLS
:INC_PHONE_CALLS
:SHOW_PHONE_CALLS
03D5: remove_text 'T_PHC01' 
03D5: remove_text 'T_PHC02' 
if $TOUHOU==C_REMI
then 00BB: text_lowpriority 'T_PHC01' 8000 ms 1 // Contacto: ~r~Flandre Scarlet~w~
else 00BB: text_lowpriority 'T_PHC02' 8000 ms 1 // Contacto: ~r~Remilia Scarlet~w~
end
return

// ---------------------------------------------------
:DEC_YELLOW_PAGES
12@-=1
if 0>12@
then 12@=3
end
jump @SHOW_YELLOW_PAGES

:INC_YELLOW_PAGES
12@+=1
if 12@>3
then 12@=0
end
jump @SHOW_YELLOW_PAGES

:SHOW_YELLOW_PAGES
if 12@ == 0
then 00BA: text_styled 'T_PH06' 3000 ms 6 // mostrar texto 'Actividades'
end
if 12@ == 1
then 00BA: text_styled 'T_PH07' 3000 ms 6 // mostrar texto 'Servicios publicos'
end
if 12@ == 2
then 00BA: text_styled 'T_PH08' 3000 ms 6 // mostrar texto 'Trabajos'
end
if 12@ == 3
then 00BA: text_styled 'T_PH09' 3000 ms 6 // mostrar texto 'Cancelar busqueda'
end
return

// ---------------------------------------------------
:DEC_ACTIVITIES
12@-=1
if 0>12@
then 12@=4
end
jump @SHOW_ACTIVITIES

:INC_ACTIVITIES
12@+=1
if 12@>4
then 12@=0
end
jump @SHOW_ACTIVITIES

:SHOW_ACTIVITIES
if 12@ == 0
then 00BA: text_styled 'T_PH10' 3000 ms 6 // mostrar texto 'Entrenamiento'
end
if 12@ == 1
then 00BA: text_styled 'T_PH11' 3000 ms 6 // mostrar texto 'Educacion y aprendizaje'
end
if 12@ == 2
then 00BA: text_styled 'T_PH12' 3000 ms 6 // mostrar texto 'Bares'
end
if 12@ == 3
then 00BA: text_styled 'T_PH13' 3000 ms 6 // mostrar texto 'Mercados'
end
if 12@ == 4
then 00BA: text_styled 'T_PH14' 3000 ms 6 // mostrar texto 'Tiendas de Ropa'
end
return

// ---------------------------------------------------
:DEC_PUBLIC_SERV
12@-=1
if 0>12@
then 12@=3
end
jump @SHOW_PUBLIC_SERV

:INC_PUBLIC_SERV
12@+=1
if 12@>3
then 12@=0
end
jump @SHOW_PUBLIC_SERV

:SHOW_PUBLIC_SERV
if 12@ == 0
then 00BA: text_styled 'T_PH15' 3000 ms 6 // mostrar texto 'Banco'
end
if 12@ == 1
then 00BA: text_styled 'T_PH16' 3000 ms 6 // mostrar texto 'Tren Subterraneo'
end
if 12@ == 2
then 00BA: text_styled 'T_PH17' 3000 ms 6 // mostrar texto 'Pay'n'spray'
end
if 12@ == 3
then 00BA: text_styled 'T_PH18' 3000 ms 6 // mostrar texto 'Ammunation'
end
return

// ---------------------------------------------------
:DEC_JOBS
12@-=1
if 0>12@
then 12@=10
end
jump @SHOW_JOBS

:INC_JOBS
12@+=1
if 12@>10
then 12@=0
end
jump @SHOW_JOBS

:SHOW_JOBS
if 12@ == 0
then 00BA: text_styled 'T_PH19' 3000 ms 6 // mostrar texto 'Paramedico'
end
if 12@ == 1
then 00BA: text_styled 'T_PH20' 3000 ms 6 // mostrar texto 'Taxista'
end
if 12@ == 2
then 00BA: text_styled 'T_PH21' 3000 ms 6 // mostrar texto 'Chofer publico'
end
if 12@ == 3
then 00BA: text_styled 'T_PH22' 3000 ms 6 // mostrar texto 'Recolector de basura'
end
if 12@ == 4
then 00BA: text_styled 'T_PH23' 3000 ms 6 // mostrar texto 'Heladero'
end
if 12@ == 5
then 00BA: text_styled 'T_PH24' 3000 ms 6 // mostrar texto 'Bombero'
end
if 12@ == 6
then 00BA: text_styled 'T_PH25' 3000 ms 6 // mostrar texto 'Policia'
end
if 12@ == 7
then 00BA: text_styled 'T_PH26' 3000 ms 6 // mostrar texto 'Distribuidor'
end
if 12@ == 8
then 00BA: text_styled 'T_PH27' 3000 ms 6 // mostrar texto 'Distribuidor por mar'
end
if 12@ == 9
then 00BA: text_styled 'T_PH28' 3000 ms 6 // mostrar texto 'Entrega de paquetes'
end
if 12@ == 10
then 00BA: text_styled 'T_PH29' 3000 ms 6 // mostrar texto 'Oficinista'
end
return

// ---------------------------------------------------

:STRENGH_MARKER
$G_MARKER_X=PORTLAND_GYM_X
$G_MARKER_Y=PORTLAND_GYM_Y
$G_MARKER_Z=PORTLAND_GYM_Z
return

:KNOWLEDGE_MARKER
$G_MARKER_X=PORTLAND_SCHOOL_X
$G_MARKER_Y=PORTLAND_SCHOOL_Y
$G_MARKER_Z=PORTLAND_SCHOOL_Z
return

:DRINK_MARKER
$G_MARKER_X=PORTLAND_BAR_X
$G_MARKER_Y=PORTLAND_BAR_Y
$G_MARKER_Z=PORTLAND_BAR_Z
return

:MARKETPLACE_MARKER
$G_MARKER_X=PORTLAND_MARKET_X
$G_MARKER_Y=PORTLAND_MARKET_Y
$G_MARKER_Z=PORTLAND_MARKET_Z
return

:CLOTH_MARKER
$G_MARKER_X=PORTLAND_CLOTH_X
$G_MARKER_Y=PORTLAND_CLOTH_Y
$G_MARKER_Z=PORTLAND_CLOTH_Z
return

:BANK_MARKER
$G_MARKER_X=PORTLAND_BANK_X
$G_MARKER_Y=PORTLAND_BANK_Y
$G_MARKER_Z=PORTLAND_BANK_Z
return

:SUBWAY_MARKER
$G_MARKER_X=PORTLAND_SUBWAY_X
$G_MARKER_Y=PORTLAND_SUBWAY_Y
$G_MARKER_Z=PORTLAND_SUBWAY_Z
return

:PAYNSPRAY_MARKER
$G_MARKER_X=PORTLAND_PAYNSPRAY_X
$G_MARKER_Y=PORTLAND_PAYNSPRAY_Y
$G_MARKER_Z=PORTLAND_PAYNSPRAY_Z
return

:AMMUNATION_MARKER
$G_MARKER_X=PORTLAND_AMMUNATION_X
$G_MARKER_Y=PORTLAND_AMMUNATION_Y
$G_MARKER_Z=PORTLAND_AMMUNATION_Z
return

:AMBULAN_MARKER
$G_MARKER_X=PORTLAND_AMBUL_X
$G_MARKER_Y=PORTLAND_AMBUL_Y
$G_MARKER_Z=PORTLAND_AMBUL_Z
return

:TAXI_MARKER
$G_MARKER_X=PORTLAND_TAXI_X
$G_MARKER_Y=PORTLAND_TAXI_Y
$G_MARKER_Z=PORTLAND_TAXI_Z
return

:BUS_MARKER
$G_MARKER_X=PORTLAND_BUS_X
$G_MARKER_Y=PORTLAND_BUS_Y
$G_MARKER_Z=PORTLAND_BUS_Z
return

:TRASHMASTER_MARKER
$G_MARKER_X=PORTLAND_TRASH_X
$G_MARKER_Y=PORTLAND_TRASH_Y
$G_MARKER_Z=PORTLAND_TRASH_Z
return

:MRWHOOPER_MARKER
$G_MARKER_X=PORTLAND_WHOOP_X
$G_MARKER_Y=PORTLAND_WHOOP_Y
$G_MARKER_Z=PORTLAND_WHOOP_Z
return

:FIREFIGHTER_MARKER
$G_MARKER_X=PORTLAND_FIRE_X
$G_MARKER_Y=PORTLAND_FIRE_Y
$G_MARKER_Z=PORTLAND_FIRE_Z
return

:POLICE_MARKER
$G_MARKER_X=PORTLAND_POLICE_X
$G_MARKER_Y=PORTLAND_POLICE_Y
$G_MARKER_Z=PORTLAND_POLICE_Z
return

:DELIVERER1_MARKER
$G_MARKER_X=PORTLAND_DELIV1_X
$G_MARKER_Y=PORTLAND_DELIV1_Y
$G_MARKER_Z=PORTLAND_DELIV1_Z
return

:DELIVERER2_MARKER
$G_MARKER_X=PORTLAND_DELIV2_X
$G_MARKER_Y=PORTLAND_DELIV2_Y
$G_MARKER_Z=PORTLAND_DELIV2_Z
return

:DELIVERER3_MARKER
$G_MARKER_X=PORTLAND_DELIV3_X
$G_MARKER_Y=PORTLAND_DELIV3_Y
$G_MARKER_Z=PORTLAND_DELIV3_Z
return

:OFFICE_MARKER
$G_MARKER_X=PORTLAND_OFFICE_X
$G_MARKER_Y=PORTLAND_OFFICE_Y
$G_MARKER_Z=PORTLAND_OFFICE_Z
return

