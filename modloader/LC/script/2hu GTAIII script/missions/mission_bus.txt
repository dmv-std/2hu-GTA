//-------------Mission x---------------
// Bus
{
0@: mission vehicle
1@, 2@, 3@: dummy position
4@, 5@, 6@: dummy
10@: state variable
11@: dummy marker/checkpoint
13@, 14@: mission time
15@: accumulated passengers
16@: timer
}

{===================================================
...
===================================================}
:M_BUS
thread 'M_BUS' 
gosub @M_BUS_START
if wasted_or_busted 
then gosub @M_BUS_FAIL
end
gosub @M_BUS_CLEANUP
end_thread 

{===================================================
START
===================================================}
:M_BUS_START
$ONMISSION=1
00D9: 0@ = actor $PLAYER_ACTOR car 
00BB: text_lowpriority 'M_WBS01' 5000 ms 1 // Presentate en la salida de la estacion para inspeccion.

if 0121:   player $PLAYER_CHAR in_zone 'AIRPORT' // Francis Intl. Airport
then $M_BUS_ISLAND3_FLAG==0
else
    if or
    0121:   player $PLAYER_CHAR in_zone 'SUB_IND' // Pike Creek (¿cala de la cúspide?)
    0121:   player $PLAYER_CHAR in_zone 'BIG_DAM' // Cochrane Dam
    then $M_BUS_ISLAND3_FLAG=1
    else
        if $M_BUS_ISLAND3_FLAG==0
        then $M_BUS_ISLAND3_FLAG=1
        else $M_BUS_ISLAND3_FLAG=0
        end
    end
end

if 03C6:   current_island == 1
then
    1@=1311.4156
    2@=-1005.441
    3@=14.8852
end
if 03C6:   current_island == 2
then
    1@=-112.9066
    2@=-1417.2635
    3@=26.1442
end
if 03C6:   current_island == 3
then
    if $M_BUS_ISLAND3_FLAG==0
    then
        1@=-717.7043
        2@=-745.4116
        3@=8.8613
    else
        1@=-1138.9237
        2@=61.0395
        3@=58.3885
    end
end
018A: 11@ = create_checkpoint_at 1@ 2@ 3@
$M_BUS_INDEX=0
01BD: 13@ = current_time_in_ms

{===================================================
Name - MAIN LOOP
===================================================}
{
Los subprocesos del trabajo de transporte público son los siguientes:
- Una vez abordado el autobus y activada la misión, deberás presentarte en la estación para inspección. Si el autobus está
muy aberiado, podrían arreglártelo.
- Será dibujado en el radar un checkpoint de la sphere invisible a donde debes ir, y otro minicheckpoint de la que le sigue,
y así hasta que llegues a una parada. Sólo las paradas tendrán spheres visibles, y en éstas tendrás que detener completamente el vehículo.
- Una vez detenido, (opcional 1): se le quita movilidad al jugador, se coloca la cámara en un punto diagonal al bus, y aparece una
barra 'pasajeros:' llenándose. (opcional 2): aparece la barra llenándose, si el jugador se mueve, se le mostrará un mensaje que diga
'no pudiste cargar a todos los pasajeros que querían abordar'
- La barra desaparece, se le muestra un mensaje de 'X pasajeros cargados'.
- Se repite el ciclo.

Condiciones de finalización de la misión:
- El jugador presiona la tecla de finalizar misión.
- Si hay nivel de búsqueda policial.
- Si el autobus tiene un daño considerable.
- Si al estar en la isla 1 o 2, han transcurrido 6 o 12 horas.
- Si al estar en la isla 3, ha finalizado el recorrido.
- Si el jugador atropella a uno de sus pasajeros (creados en el código).
}
:M_BUS_LOOP
wait 0 ms

if 10@==0
then
    if 0103:   actor $PLAYER_ACTOR stopped_near_point_in_car 1@ 2@ 3@ radius 4.0 4.0 6.0 sphere 1
    then
        0164: disable_marker 11@ 
        00BB: text_lowpriority 'M_WBS02' 4000 ms 1 // Comprobando unidad... espere...
        16@=0
        10@=1
    end
end

if 10@==1
then
    if 80EE:   not actor $PLAYER_ACTOR 0 1@ 2@ radius 6.0 6.0
    then
        00BB: text_lowpriority 'M_WBS03' 4000 ms 1 // Aun no ha finalizado la inspeccion. regresa aqui.
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=0
    end
    if 16@>=8000
    then
        if 8185:   not car 0@ health >= 300
        then
            00BB: text_lowpriority 'M_WBS04' 4000 ms 1 // Reparando unidad... Espere...
            16@=0
            10@=2
        else
            00BB: text_lowpriority 'M_WBS07' 6000 ms 1 // Unidad inspeccionada con exito. Ya puedes incorporarte en la ruta comercial.
            gosub @M_BUS_GET_NEXT_LOCATION
            018A: 11@ = create_checkpoint_at 1@ 2@ 3@
            10@=4
        end
    end
end

if 10@==2
then
    if 80EE:   not actor $PLAYER_ACTOR 0 1@ 2@ radius 6.0 6.0
    then
        00BB: text_lowpriority 'M_WBS05' 4000 ms 1 // No ha finalizado la reparacion, vuelve aqui.
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=3
    end
    if 16@>=8000
    then
        0224: set_car 0@ health_to 1000
        00BA: text_styled 'M_WBS06' 4000 ms 6 // Unidad reparada
        00BB: text_lowpriority 'M_WBS07' 6000 ms 1 // Unidad inspeccionada con exito. Ya puedes incorporarte en la ruta comercial.
        gosub @M_BUS_GET_NEXT_LOCATION
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=4
    end
end

if 10@==3
then
    if 0103:   actor $PLAYER_ACTOR stopped_near_point_in_car 1@ 2@ 3@ radius 4.0 4.0 6.0 sphere 1
    then
        0164: disable_marker 11@ 
        00BB: text_lowpriority 'M_WBS03' 4000 ms 1 // Reparando unidad... Espere...
        16@=0
        10@=2
    end
end

if 10@==4
then
    if 0103:   actor $PLAYER_ACTOR stopped_near_point_in_car 1@ 2@ 3@ radius 4.0 4.0 6.0 sphere 1
    then
        0164: disable_marker 11@ 
        // Condiciones para repetir ciclo de forma normal
        if or
        03C6:   current_island == 1
        03C6:   current_island == 2
        then 10@=5
        end
        if and
        03C6:   current_island == 3
        not $M_BUS_INDEX>16
        then 10@=5
        end
        
        // Condiciones para romper ciclo y caer en estado especial que reinicializa index y flag
        if and
        03C6:   current_island == 3
        $M_BUS_INDEX>16
        then 10@=7
        $dummy=0
        end
    end
end

if 10@==5
then
    0209: 4@ = random_int 0 6
    if 4@==0
    then
        00BB: text_lowpriority 'M_WBS18' 4000 ms 1 // No hay pasajeros que recoger en esta parada.
        gosub @M_BUS_GET_NEXT_LOCATION
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=4
    else
        $dummy=0
        03C4: set_status_text_to $dummy 1 'M_WBS08' // Pasajeros:
        10@=6
    end
end

if 10@==6
then
    if 80EE:   not actor $PLAYER_ACTOR 0 1@ 2@ radius 6.0 6.0
    then
        0151: remove_status_text $dummy 
        0068: 4@ *= $dummy // integer values
        0014: 4@ /= 100 // integer values
        if 4@>1
        then 01E4: text_1number_lowpriority 'M_WBS11' 4@ 6000 ms 1 // Por andar con prisa no haz dejado abordar a todos los pasajeros de esta parada. Subieron ~1~ pasajeros.
        else
            if 4@==1
            then 01E4: text_1number_lowpriority 'M_WBS12' 4@ 6000 ms 1 // Por andar con prisa no haz dejado abordar a todos los pasajeros de esta parada. Subio ~1~ pasajero.
            else 00BB: text_lowpriority 'M_WBS13' 4000 ms 1 // Por andar con prisa no haz dejado abordar a ningun pasajero.
            end
        end
        0058: 15@ += 4@ // integer values
        gosub @M_BUS_GET_NEXT_LOCATION
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=4
    end
    Inc($dummy)
    if $dummy>=100
    then
        0151: remove_status_text $dummy 
        if 4@>1
        then 01E4: text_1number_lowpriority 'M_WBS09' 4@ 6000 ms 1 // Haz subido ~1~ pasajeros a la unidad.
        else 01E4: text_1number_lowpriority 'M_WBS10' 4@ 6000 ms 1 // Haz subido ~1~ pasajero a la unidad.
        end
        0058: 15@ += 4@ // integer values
        gosub @M_BUS_GET_NEXT_LOCATION
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=4
    end
end

if 10@==7
then
    03C4: set_status_text_to $dummy 1 'M_WBS19' // Bajando:
    10@=8
end

if 10@==8
then
    if 80EE:   not actor $PLAYER_ACTOR 0 1@ 2@ radius 4.0 4.0
    then
        0151: remove_status_text $dummy 
        00BB: text_lowpriority 'M_WBS20' 4000 ms 1 // Los pasajeros no han terminado de desbordar el autobus.
        018A: 11@ = create_checkpoint_at 1@ 2@ 3@
        10@=4
    end
    Inc($dummy)
    if $dummy>=100
    then
        00BB: text_lowpriority 'M_WBS21' 4000 ms 1 // Todos los pasajeros han bajado en la parada terminal.
        // Reiniciando ciclo
        $M_BUS_INDEX=1
        if $M_BUS_ISLAND3_FLAG==0
        then $M_BUS_ISLAND3_FLAG=1
        else $M_BUS_ISLAND3_FLAG=0
        end
        10@=4
    end
end

if 010F:   player $PLAYER_CHAR wanted_level > 0
then
    00BB: text_lowpriority 'M_WBS15' 4000 ms 1 // ~r~Bajo persecucion policial no podras prestar servicio.
    jump @M_BUS_PASSED
end

if 80DB:   not actor $PLAYER_ACTOR in_car 0@
then
    00BB: text_lowpriority 'M_WBS14' 3000 ms 1 // ~r~Haz abandonado la unidad.
    jump @M_BUS_PASSED
end

01BD: 14@ = current_time_in_ms 
0060: 14@ -= 13@ // integer values 
if 14@>360000 // 6 min * 60 sec * 1000 ms --> 6 horas en el juego
then
    gosub @M_BUS_SHOW_STATISTICS
    gosub @M_BUS_EXPERIENCE
    00BA: text_styled 'M_WT' 8000 ms 5 // Ciclo de 6 horas finalizado
    00BA: text_styled 'M_EXP' 6000 ms 6  // + EXP
    //03E5: text_box 'M_WL' // Recuerda que puedes finalizar cuando lo desees pulsando ~h~boton ~k~~TOGGLE_SUBMISSIONS~~w~.
    01BD: 13@ = current_time_in_ms
end

if $energy == 0
then 00BB: text_lowpriority 'T_EN2' 5000 ms 1 // ~r~Te haz quedado sin energia para seguir trabajando. Necesitas descanzar.
     jump @M_BUS_PASSED
end

gosub @M_BUS_CANCEL
if 10@==100
then jump @M_BUS_PASSED
end
jump @M_BUS_LOOP

{===================================================
LOCAL SUBROUTINES
===================================================}
:M_BUS_CANCEL
0293: $CURRENT_CONTROLS = current_controls 
if not $CURRENT_CONTROLS == 3 // integer values 
then
    if and
    00E1:   pad 0 key_pressed 19 
    $ONMISSION == 1 // integer values 
    then 10@=99
    end
else
    if and
    00E1:   pad 0 key_pressed 14 
    $ONMISSION == 1 // integer values 
    then 10@=99
    end
end

if 10@==99
then
    0293: $CURRENT_CONTROLS = current_controls 
    if not $CURRENT_CONTROLS == 3 // integer values 
    then
        if and
        80E1:   not pad 0 key_pressed 19 
        $ONMISSION == 1 // integer values 
        then 10@=100
        end
    else
        if and
        80E1:   not pad 0 key_pressed 14 
        $ONMISSION == 1 // integer values 
        then 10@=100
        end
    end
end
return

:M_BUS_GET_NEXT_LOCATION
if 03C6:   current_island == 1
then gosub @M_BUS_GET_NEXT_LOCATION1
end
if 03C6:   current_island == 2
then gosub @M_BUS_GET_NEXT_LOCATION2
end
if 03C6:   current_island == 3
then gosub @M_BUS_GET_NEXT_LOCATION3
end
return

:M_BUS_GET_NEXT_LOCATION1
if $M_BUS_INDEX>12
then $M_BUS_INDEX=0
end
if $M_BUS_INDEX==0
then
    1@=1344.6593
    2@=-880.4171
    3@=14.9727
end
if $M_BUS_INDEX==1
then
    1@=1280.6864
    2@=-667.6746
    3@=19.9699
end
if $M_BUS_INDEX==2
then
    1@=1265.7478
    2@=-508.899
    3@=32.0356
end
if $M_BUS_INDEX==3
then
    1@=1380.8789
    2@=-371.4507
    3@=50.0701
end
if $M_BUS_INDEX==4
then
    1@=1216.8468
    2@=-338.6598
    3@=26.0805
end
if $M_BUS_INDEX==5
then
    1@=1200.9376
    2@=-77.113
    3@=11.802
end
if $M_BUS_INDEX==6
then
    1@=927.128
    2@=-33.6484
    3@=7.473
end
if $M_BUS_INDEX==7
then
    1@=905.5717
    2@=-242.7119
    3@=4.9724
end
if $M_BUS_INDEX==8
then
    1@=810.8345
    2@=-473.9149
    3@=14.8227
end
if $M_BUS_INDEX==9
then
    1@=950.6789
    2@=-503.9441
    3@=14.9727
end
if $M_BUS_INDEX==10
then
    1@=895.5507
    2@=-735.7225
    3@=14.9727
end
if $M_BUS_INDEX==11
then
    1@=909.8315
    2@=-1031.7238
    3@=7.8588
end
if $M_BUS_INDEX==12
then
    1@=1195.1383
    2@=-1104.4337
    3@=11.7102
end
Inc($M_BUS_INDEX)
return

:M_BUS_GET_NEXT_LOCATION2
if $M_BUS_INDEX>25
then $M_BUS_INDEX=0
end
if $M_BUS_INDEX==0
then
    1@=-162.0989
    2@=-1516.5657
    3@=26.0181
end
if $M_BUS_INDEX==1
then
    1@=51.9271
    2@=-1404.3738
    3@=26.1681
end
if $M_BUS_INDEX==2
then
    1@=92.7781
    2@=-1094.7924
    3@=26.1681
end
if $M_BUS_INDEX==3
then
    1@=176.8315
    2@=-1621.4899
    3@=26.1682
end
if $M_BUS_INDEX==4
then
    1@=325.3371
    2@=-1457.7869
    3@=26.1682
end
if $M_BUS_INDEX==5
then
    1@=516.0174
    2@=-1487.2673
    3@=16.1682
end
if $M_BUS_INDEX==6
then
    1@=431.8803
    2@=-1076.5756
    3@=26.1536
end
if $M_BUS_INDEX==7
then
    1@=431.8117
    2@=-422.9415
    3@=22.1994
end
if $M_BUS_INDEX==8
then
    1@=457.1408
    2@=-155.2841
    3@=21.3306
end
if $M_BUS_INDEX==9
then
    1@=204.4391
    2@=-51.2887
    3@=21.274
end
if $M_BUS_INDEX==10
then
    1@=96.5503
    2@=-188.6115
    3@=16.1682
end
if $M_BUS_INDEX==11
then
    1@=241.7036
    2@=-462.4485
    3@=26.1681
end
if $M_BUS_INDEX==12
then
    1@=174.1728
    2@=-686.3372
    3@=26.1681
end
if $M_BUS_INDEX==13
then
    1@=-18.3224
    2@=-556.8781
    3@=20.2501
end
if $M_BUS_INDEX==14
then
    1@=-37.2341
    2@=-796.1126
    3@=26.1884
end
if $M_BUS_INDEX==15
then
    1@=-88.2754
    2@=-920.9867
    3@=26.1772
end
if $M_BUS_INDEX==16
then
    1@=109.299
    2@=-946.7091
    3@=26.1682
end
if $M_BUS_INDEX==17
then
    1@=247.1729
    2@=-1171.804
    3@=26.1681
end
if $M_BUS_INDEX==18
then
    1@=315.7823
    2@=-983.6863
    3@=25.214
end
if $M_BUS_INDEX==19
then
    1@=575.6117
    2@=-373.8888
    3@=20.8325
end
if $M_BUS_INDEX==20
then
    1@=499.488
    2@=7.1698
    3@=5.561
end
if $M_BUS_INDEX==21
then
    1@=185.5095
    2@=93.7798
    3@=16.1681
end
if $M_BUS_INDEX==22
then
    1@=-189.1645
    2@=-99.8145
    3@=16.1682
end
if $M_BUS_INDEX==23
then
    1@=-113.2636
    2@=-434.1212
    3@=16.1681
end
if $M_BUS_INDEX==24
then
    1@=-68.2066
    2@=-902.9479
    3@=15.9074
end
if $M_BUS_INDEX==25
then
    1@=-199.664
    2@=-1443.4967
    3@=26.0181
end
Inc($M_BUS_INDEX)
return

:M_BUS_GET_NEXT_LOCATION3
if $M_BUS_ISLAND3_FLAG==0
then
    if $M_BUS_INDEX==0
    then
        1@=-693.7571
        2@=-733.9798
        3@=8.8614
    end
    if $M_BUS_INDEX==1
    then
        1@=-730.3444
        2@=-471.8376
        3@=8.8613
    end
    if $M_BUS_INDEX==2
    then
        1@=-939.5825
        2@=-274.0634
        3@=33.8614
    end
    if $M_BUS_INDEX==3
    then
        1@=-805.0512
        2@=-174.7649
        3@=32.99
    end
    if $M_BUS_INDEX==4
    then
        1@=-1107.4653
        2@=-92.1195
        3@=46.4783
    end
    if $M_BUS_INDEX==5
    then
        1@=-1110.3145
        2@=87.268
        3@=54.9832
    end
    if $M_BUS_INDEX==6
    then
        1@=-662.9518
        2@=217.4818
        3@=54.9165
    end
    if $M_BUS_INDEX==7
    then
        1@=-290.0403
        2@=391.2696
        3@=80.5992
    end
    if $M_BUS_INDEX==8
    then
        1@=-717.9543
        2@=167.4899
        3@=29.5662
    end
    if $M_BUS_INDEX==9
    then
        1@=-620.0709
        2@=-19.0539
        3@=18.8613
    end
    if $M_BUS_INDEX==10
    then
        1@=-440.2925
        2@=-41.5096
        3@=3.8613
    end
    if $M_BUS_INDEX==11
    then
        1@=-640.2112
        2@=-147.1317
        3@=3.8614
    end
    if $M_BUS_INDEX==12
    then
        1@=-926.934
        2@=282.2478
        3@=33.8613
    end
    if $M_BUS_INDEX==13
    then
        1@=-879.6729
        2@=489.9938
        3@=60.0479
    end
    if $M_BUS_INDEX==14
    then
        1@=-1155.5668
        2@=499.1239
        3@=68.2517
    end
    if $M_BUS_INDEX==15
    then
        1@=-1270.0413
        2@=54.3878
        3@=67.0552
    end
    if $M_BUS_INDEX==16
    then
        1@=-1138.9237
        2@=61.0395
        3@=58.3885
    end
    Inc($M_BUS_INDEX)
else
    if $M_BUS_INDEX==0
    then
        1@=-1259.9666
        2@=93.9992
        3@=68.7123
    end
    if $M_BUS_INDEX==1
    then
        1@=-1135.6244
        2@=486.7297
        3@=68.215
    end
    if $M_BUS_INDEX==2
    then
        1@=-900.0271
        2@=433.6043
        3@=48.9392
    end
    if $M_BUS_INDEX==3
    then
        1@=-924.1689
        2@=302.4952
        3@=33.8613
    end
    if $M_BUS_INDEX==4
    then
        1@=-735.8939
        2@=-50.1135
        3@=3.8614
    end
    if $M_BUS_INDEX==5
    then
        1@=-419.8538
        2@=-36.4484
        3@=3.8613
    end
    if $M_BUS_INDEX==6
    then
        1@=-600.0002
        2@=-30.3311
        3@=18.8613
    end
    if $M_BUS_INDEX==7
    then
        1@=-627.8741
        2@=147.4902
        3@=38.4243
    end
    if $M_BUS_INDEX==8
    then
        1@=-269.7703
        2@=351.7997
        3@=78.8613
    end
    if $M_BUS_INDEX==9
    then
        1@=-333.5758
        2@=253.7146
        3@=61.1688
    end
    if $M_BUS_INDEX==10
    then
        1@=-569.0873
        2@=237.6403
        3@=62.453
    end
    if $M_BUS_INDEX==11
    then
        1@=-1104.9731
        2@=107.6348
        3@=53.9309
    end
    if $M_BUS_INDEX==12
    then
        1@=-1123.1104
        2@=-101.5843
        3@=45.5603
    end
    if $M_BUS_INDEX==13
    then
        1@=-831.9791
        2@=-132.4441
        3@=33.7114
    end
    if $M_BUS_INDEX==14
    then
        1@=-985.162
        2@=-251.5598
        3@=33.8614
    end
    if $M_BUS_INDEX==15
    then
        1@=-740.754
        2@=-538.9571
        3@=8.8614
    end
    if $M_BUS_INDEX==16
    then
        1@=-717.7043
        2@=-745.4116
        3@=8.8613
    end
    Inc($M_BUS_INDEX)
end
return

:M_BUS_EXPERIENCE
0008: $l_exp += 5 // integer values 
0209: 6@ = random_int 6 13
0058: $m_choffeur_exp += 6@ // integer values 
create_thread @DRAW_STATS ID_LABORAL_EXP
create_thread @DRAW_STATS ID_CHAUFFEUR_EXP
return

:M_BUS_SHOW_STATISTICS
if 001F:   15@ > $M_BUS_MAX_PSSGS // integer values 
then 0084: $M_BUS_MAX_PSSGS = 15@ // integer values and handles 
end
0109: player $PLAYER_CHAR money += 15@
{
Convirtiendo el dato 14@ que corresponde a la cantidad de milisegundos transcurridos durante la mision
en su equivalente en minutos:segundos
}
0014: 14@ /= 100 // integer values 
0084: 4@ = 14@ // integer values and handles 
0014: 4@ /= 600 // integer values
008D: 6@ = integer_to_float 14@ 
0015: 6@ /= 600.0 // floating-point values 
008D: 5@ = integer_to_float 4@ 
0061: 6@ -= 5@ // floating-point values 
0011: 6@ *= 60.0 // floating-point values 
008C: 6@ = float_to_integer 6@
if 6@>9 
then 0302: text_4numbers 'M_WBS16' 15@ 4@ 6@ $M_BUS_MAX_PSSGS 10000 ms 5 //Pasajeros trasladados: ~1~, Tiempo de servicio: ~1~:~1~, Record de pasajeros trasladados: ~1~
else 0302: text_4numbers 'M_WBS17' 15@ 4@ 6@ $M_BUS_MAX_PSSGS 10000 ms 5 //Pasajeros trasladados: ~1~, Tiempo de servicio: ~1~:0~1~, Record de pasajeros trasladados: ~1~
end
15@=0
return

{===================================================
END OF THE MISSION
===================================================}
:M_BUS_PASSED
00BA: text_styled 'M_WO' 5000 ms 5  // Jornada finalizada
01E3: text_1number_styled 'TSCORE' 15@ 6000 ms 6  // EARNINGS: $~1~
gosub @M_BUS_SHOW_STATISTICS
return

:M_BUS_FAIL
return

:M_BUS_CLEANUP
$ONMISSION = 0 // integer values
0164: disable_marker 11@
0151: remove_status_text $dummy
00D8: mission_cleanup 
return


