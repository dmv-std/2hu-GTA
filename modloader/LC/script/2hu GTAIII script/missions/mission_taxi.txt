//-------------Mission 2---------------
// Taxi Missions
{
0@: player car
1@, 2@, 3@: dummy position
4@, 5@, 6@: dummy
7@, 8@, 9@: dummy position 2
10@: state variable
11@: passenger
12@: dummy marker
13@, 14@, 15@: dummy
16@, 17@: timers
}

{===================================================
...
===================================================}
:M_TAXI
thread 'TAXI' 
gosub @M_TAXI_START
if wasted_or_busted 
then gosub @M_TAXI_FAIL
end
gosub @M_TAXI_CLEANUP
end_thread 

{===================================================
START
===================================================}
:M_TAXI_START
$ONMISSION=1
$M_TAXI_MODE=0 // 0: no bonus, no speed bonus, no timer
00D9: 0@ = actor $PLAYER_ACTOR car
$M_TIMER=0 
$M_TIMER_FLAG=0
$FARES_INROW_COMPLETED=0
$TOTAL_TAXI_EARNINGS = 0 // integer values 
$FARES_FOR_NEXT_INROW_BONUS = 5 // integer values 
$FARES_INROW_BONUS = 25 // integer values 
03C4: set_status_text_to $FARES_INROW_COMPLETED 0 'FARES'  // FARES:
00BC: text_highpriority 'TAXI1' 1500 ms 1  // ~g~Look for a fare.
03E5: text_box 'TAXIH1'  // Stop near a highlighted pedestrian to pick them up then drive them to their destination before the time runs out.
0216: set_car 0@ taxi_available_light_to 1 
01BD: $m_mission_time_0 = current_time_in_ms 

{===================================================
Name - MAIN LOOP
===================================================}
:M_TAXI_LOOP
wait 0 ms

if 10@ == 0
then
    if 03C6:   current_island == 1 
    then 02DD: set_actor 11@ creation_zone 'IND_ZON'
    end
    if 03C6:   current_island == 2 
    then 02DD: set_actor 11@ creation_zone 'COM_ZON'
    end
    if 03C6:   current_island == 3 
    then 02DD: set_actor 11@ creation_zone 'SUB_ZON'
    end
    if not 11@ == -1 // integer values 
    then
        009F: set_actor 11@ objective_to-1 
        01ED: reset_actor 11@ flags 
        0291: unknown_actor 11@ unknown_behavior_flag 0 
        0187: 12@ = create_marker_above_actor 11@ 
        0365: set_actor 11@ objective_to-29 
        10@=1
    end
end

if or
10@ == 1
10@ == 2
then
    if 80FD:   not player $PLAYER_CHAR 0 11@ in_car radius 90.0 90.0 20.0 
    then
        0164: disable_marker 12@ 
        01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
        10@=0
    end
end

if 10@ == 1
then
    if and
    00FD:   player $PLAYER_CHAR 0 11@ in_car radius 7.0 7.0 2.0 
    01C1:   car 0@ stopped
    then
        0319: set_actor 11@ wander_state_to 1 
        01D4: actor 11@ go_to_car 0@ and_enter_it_as_a_passenger
        10@=2 
    end
    020F: actor 11@ look_at_player $PLAYER_CHAR
end

if 10@ == 2
then
    if 00DB:   actor 11@ in_car 0@
    then
        0164: disable_marker 12@
        0216: set_car 0@ taxi_available_light_to 0 
        00A0: store_actor 11@ position_to 13@ 14@ 15@ 
        gosub @M_TAXI_GET_DESTINATION // genera 1@, 2@, 3@, 7@, 8@ y 9@ (posicion cubo)
        gosub @M_TAXI_CALCULATE_EARNINGS // salida: 4@ y 5@ (calculadas a partir de 13@, 14@ y 15@)   // 4@: bonus de tiempo, 5@: ganancia
        if $M_TAXI_MODE == 1 // With Bonus
        then gosub @M_TAXI_CALCULATE_TIMER
        end
        10@=3
    end
end

if 10@ == 3
then
    if 01AC:   car 0@ stopped 1 1@ 2@ 3@ 7@ 8@ 9@
    then
        gosub @M_TAXI_SHOW_BONUS
        gosub @M_TAXI_SHOW_EARNINGS
        gosub @M_TAXI_JOB_EXPERIENCE
        0164: disable_marker 12@ 
        01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
        0216: set_car 0@ taxi_available_light_to 1
        00BD: text_mediumpriority 'TAXI1' 1500 ms 1  // ~g~Look for a fare.
        10@=0
    end
    if and
    01F4:   car 0@ flipped 
    01C1:   car 0@ stopped 
    then
        00BC: text_highpriority 'TAXI3' 5000 ms 1  // ~r~Your passenger fled in terror!
        if 8118:   not actor 11@ dead 
        then 0193: set_actor 11@ objective_to_act_like_ped
        end
        10@=21 // 'M_TAXI_PED_FLIES_FROM_CAR' state
    end
end

// Coche destruido o averiado
if 0119:   car 0@ wrecked 
then jump @M_TAXI_PASSED
end

// Cliente muerto
if and
10@>0
3>=10@
then if 0118:   actor 11@ dead 
     then 00BC: text_highpriority 'M_WTX3' 5000 ms 1  // ~r~Tu cliente esta muerto!
          jump @M_TAXI_PASSED
     end
end

// Jugador se salió del vehículo
if 80DC:   not player $PLAYER_CHAR driving 0@ 
then jump @M_TAXI_PASSED
end

// El tiempo (modo Bonus) se agotó
if and
  $M_TIMER_FLAG == 1 // integer values 
  $M_TIMER == 0 // integer values 
then
    00BC: text_highpriority 'TAXI2' 5000 ms 1  // ~r~You're out of time!
    jump @M_TAXI_PASSED
end

{if $energy == 0
then 00BB: text_lowpriority 'T_EN2' 5000 ms 1 // ~r~Te haz quedado sin energia para seguir trabajando. Necesitas descanzar.
     jump @M_TAXI_PASSED
end}

// Tiempo de misión sobrepasó las 6 horas
01BD: $m_mission_time_1 = current_time_in_ms 
0060: $m_mission_time_1 -= $m_mission_time_0 // integer values 
if $m_mission_time_1>360000 // 6 min * 60 sec * 1000 ms --> 6 horas en el juego
then gosub @M_TAXI_SHOW_STATISTICS
     jump @M_TAXI_PASSED
end

// El jugador presionó el botón de abandonar misión
gosub @M_TAXI_CANCEL
if 10@==100
then jump @M_TAXI_PASSED
end

gosub @M_TAXI_CAR_DAMAGED
gosub @M_TAXI_PED_FLIES_FROM_CAR

jump @M_TAXI_LOOP

{===================================================
LOCAL SUBROUTINES
===================================================}
:M_TAXI_CANCEL
if and
00E1:   pad 0 key_pressed K_SUBMISSIONS 
$ONMISSION == 1 // integer values 
then 10@=99
end
if 10@==99
then
    if and
    80E1:   not pad 0 key_pressed K_SUBMISSIONS 
    $ONMISSION == 1 // integer values 
    then 10@=100
    end
end
return

:M_TAXI_CAR_DAMAGED
if 8185:   not car 0@ health >= 500 
then
    0216: set_car 0@ taxi_available_light_to 0 
    00BC: text_highpriority 'TAXI7' 4000 ms 1  // ~r~Your car is trashed, get it repaired.
    0164: disable_marker 12@ 
    //01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
    if 03C6:   current_island == 1 
    then 02A8: 12@ = create_marker I_PAY_N_SPRAY at 925.0 -359.5 -100.0
    end
    if 03C6:   current_island == 2 
    then 02A8: 12@ = create_marker I_PAY_N_SPRAY at 379.0 -493.75 -100.0
    end
    if 03C6:   current_island == 3 
    then 02A8: 12@ = create_marker I_PAY_N_SPRAY at -1128.0 32.5 -100.0
    end
    10@=20
end
if 10@ == 20
then
    if 0185:   car 0@ health >= 500
    then
        0164: disable_marker 12@
        if 00DB:   actor 11@ in_car 0@
        then
            gosub @M_TAXI_CALCULATE_EARNINGS // salida: 4@ y 5@ (calculadas a partir de 13@, 14@ y 15@)
            10@=3
        else
            0216: set_car 0@ taxi_available_light_to 1 
            00BC: text_highpriority 'TAXI1' 1500 ms 1  // ~g~Look for a fare.
            10@=0
        end
    end
end
return

:M_TAXI_PED_FLIES_FROM_CAR
if 10@ == 21
then
    if 8118:   not actor 11@ dead 
    then
        if 8119:   not car 0@ wrecked 
        then
            if 00DB:   actor 11@ in_car 0@ 
            then
                01D3: actor 11@ leave_car 0@ 
                if 0256:   player $PLAYER_CHAR defined 
                then 01B4: set_player $PLAYER_CHAR frozen_state 0
                end
                while 00DB:   actor 11@ in_car 0@ 
                    wait 0 
                end
            end
        end
    end
    10@=22
end
if 10@ == 22
then
    01B4: set_player $PLAYER_CHAR frozen_state 1
    10@=20
end
return

:M_TAXI_GET_DESTINATION
wait 0
if 03C6:   current_island == 1 
then
    0209: 4@ = random_int 1 11 
    if 4@ == 1 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'REDLIGH'  // Red Light District
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE1' 5000 ms 1  // ~g~Destination ~w~'Meeouch Sex Kitten Club' ~g~in Redlight.
        1@ = 936.25 // floating-point values 
        2@ = -462.5625 // floating-point values 
        3@ = 14.375 // floating-point values 
        7@ = 913.6875 // floating-point values 
        8@ = -456.0 // floating-point values 
        9@ = 16.375 // floating-point values
    end
    if 4@ == 2 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'S_VIEW'  // Portland View
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE2' 5000 ms 1  // ~g~Destination ~w~'Supa Save' ~g~in Portland View.
        1@ = 1268.5 // floating-point values 
        2@ = -616.375 // floating-point values 
        3@ = 11.6875 // floating-point values 
        7@ = 1288.25 // floating-point values 
        8@ = -627.5625 // floating-point values 
        9@ = 13.6875 // floating-point values
    end
    if 4@ == 3 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'CHINA'  // Chinatown
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE3' 5000 ms 1  // ~g~Destination ~w~'old school hall' ~g~in Chinatown.
        1@ = 1008.875 // floating-point values 
        2@ = -871.875 // floating-point values 
        3@ = 14.375 // floating-point values 
        7@ = 995.25 // floating-point values 
        8@ = -881.875 // floating-point values 
        9@ = 16.375 // floating-point values
    end
    if 4@ == 4 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'PORT_W'  // Callahan Point
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE4' 5000 ms 1  // ~g~Destination ~w~'Greasy Joe's Cafe' ~g~in Callahan Point.
        1@ = 849.25 // floating-point values 
        2@ = -990.0625 // floating-point values 
        3@ = 4.5 // floating-point values 
        7@ = 869.375 // floating-point values 
        8@ = -1002.563 // floating-point values 
        9@ = 6.5 // floating-point values
    end
    if 4@ == 5 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'REDLIGH'  // Red Light District
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE5' 5000 ms 1  // ~g~Destination ~w~'AmmuNation' ~g~in Redlight.
        1@ = 1065.375 // floating-point values 
        2@ = -394.0625 // floating-point values 
        3@ = 14.25 // floating-point values 
        7@ = 1057.875 // floating-point values 
        8@ = -408.875 // floating-point values 
        9@ = 16.25 // floating-point values
    end
    if 4@ == 6 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'LITTLEI'  // Saint Mark's
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE6' 5000 ms 1  // ~g~Destination ~w~'Easy Credit Autos' ~g~in Saint Mark's.
        1@ = 1207.0 // floating-point values 
        2@ = -122.0 // floating-point values 
        3@ = 14.0 // floating-point values 
        7@ = 1224.0 // floating-point values 
        8@ = -108.0 // floating-point values 
        9@ = 16.0 // floating-point values
    end
    if 4@ == 7 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'REDLIGH'  // Red Light District
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE7' 5000 ms 1  // ~g~Destination ~w~'Woody's topless bar' ~g~in Redlight.
        1@ = 912.875 // floating-point values 
        2@ = -419.0 // floating-point values 
        3@ = 14.0 // floating-point values 
        7@ = 919.5 // floating-point values 
        8@ = -401.25 // floating-point values 
        9@ = 16.0 // floating-point values
    end
    if 4@ == 8 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'LITTLEI'  // Saint Mark's
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE8' 5000 ms 1  // ~g~Destination ~w~'Marcos Bistro' ~g~in Saint Mark's.
        1@ = 1345.75 // floating-point values 
        2@ = -461.75 // floating-point values 
        3@ = 49.375 // floating-point values 
        7@ = 1334.688 // floating-point values 
        8@ = -447.0 // floating-point values 
        9@ = 51.375 // floating-point values
    end
    if 4@ == 9 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'PORT_E'  // Portland Harbor
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE9' 5000 ms 1  // ~g~Destination ~w~'import export garage' ~g~in Portland Harbor.
        1@ = 1475.0 // floating-point values 
        2@ = -686.0 // floating-point values 
        3@ = 11.25 // floating-point values 
        7@ = 1485.25 // floating-point values 
        8@ = -667.5 // floating-point values 
        9@ = 13.25 // floating-point values
    end
    if 4@ == 10 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'CHINA'  // Chinatown
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE10' 5000 ms 1  // ~g~Destination ~w~'Punk Noodles' ~g~in Chinatown.
        1@ = 1039.063 // floating-point values 
        2@ = -660.0625 // floating-point values 
        3@ = 14.375 // floating-point values 
        7@ = 1043.875 // floating-point values 
        8@ = -647.875 // floating-point values 
        9@ = 16.375 // floating-point values
    end
end
if 03C6:   current_island == 2 
then
    0209: 4@ = random_int 11 21 
    if 4@ == 11 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'CONSTRU'  // Fort Staunton
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE11' 5000 ms 1  // ~g~Destination ~w~'Construction site' ~g~in Fort staunton.
        1@ = 441.8125 // floating-point values 
        2@ = -193.0 // floating-point values 
        3@ = 20.3125 // floating-point values 
        7@ = 447.375 // floating-point values 
        8@ = -201.875 // floating-point values 
        9@ = 22.1875 // floating-point values
    end
    if 4@ == 12 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'STADIUM'  // Aspatria
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE12' 5000 ms 1  // ~g~Destination ~w~'Football Stadium' ~g~in Aspatria.
        1@ = -27.0 // floating-point values 
        2@ = -269.5625 // floating-point values 
        3@ = 14.9375 // floating-point values 
        7@ = -11.6875 // floating-point values 
        8@ = -278.875 // floating-point values 
        9@ = 16.875 // floating-point values
    end
    if 4@ == 13 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'SHOPING'  // Bedford Point
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE13' 5000 ms 1  // ~g~Destination ~w~'The Church' ~g~in Bedford Point
        1@ = 22.0625 // floating-point values 
        2@ = -1136.938 // floating-point values 
        3@ = 25.125 // floating-point values 
        7@ = 28.9375 // floating-point values 
        8@ = -1125.625 // floating-point values 
        9@ = 27.0625 // floating-point values
    end
    if 4@ == 14 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'YAKUSA'  // Torrington
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE14' 5000 ms 1  // ~g~Destination ~w~'The Casino' ~g~in Torrington
        1@ = 421.375 // floating-point values 
        2@ = -1390.813 // floating-point values 
        3@ = 24.9375 // floating-point values 
        7@ = 415.4375 // floating-point values 
        8@ = -1401.188 // floating-point values 
        9@ = 26.875 // floating-point values
    end
    if 4@ == 15 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'UNIVERS'  // Liberty Campus
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE15' 5000 ms 1  // ~g~Destination ~w~'Liberty University' ~g~in Liberty Campus
        1@ = 183.125 // floating-point values 
        2@ = -215.5 // floating-point values 
        3@ = 17.0 // floating-point values 
        7@ = 167.1875 // floating-point values 
        8@ = -221.1875 // floating-point values 
        9@ = 19.25 // floating-point values
    end
    if 4@ == 16 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'PARK'  // Belleville Park
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE16' 5000 ms 1  // ~g~Destination ~w~'Shopping Mall' ~g~in Belleville Park Area
        1@ = 193.75 // floating-point values 
        2@ = -626.1875 // floating-point values 
        3@ = 25.0625 // floating-point values 
        7@ = 180.8125 // floating-point values 
        8@ = -616.5 // floating-point values 
        9@ = 27.0625 // floating-point values
    end
    if 4@ == 17 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'COM_EAS'  // Newport
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE17' 5000 ms 1  // ~g~Destination ~w~'Museum' ~g~in Newport
        1@ = 326.0625 // floating-point values 
        2@ = -1001.688 // floating-point values 
        3@ = 29.0 // floating-point values 
        7@ = 316.25 // floating-point values 
        8@ = -1012.375 // floating-point values 
        9@ = 24.4375 // floating-point values
    end
    if 4@ == 18 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'YAKUSA'  // Torrington
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE18' 5000 ms 1  // ~g~Destination ~w~'AmCo Building' ~g~in Torrington
        1@ = 246.1875 // floating-point values 
        2@ = -1192.75 // floating-point values 
        3@ = 24.6875 // floating-point values 
        7@ = 256.625 // floating-point values 
        8@ = -1184.125 // floating-point values 
        9@ = 26.6875 // floating-point values
    end
    if 4@ == 19 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'SHOPING'  // Bedford Point
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE19' 5000 ms 1  // ~g~Destination ~w~'Bolt Burgers' ~g~in Bedford Point
        1@ = 28.6875 // floating-point values 
        2@ = -1066.625 // floating-point values 
        3@ = 26.6875 // floating-point values 
        7@ = 34.0625 // floating-point values 
        8@ = -1078.938 // floating-point values 
        9@ = 24.9375 // floating-point values
    end
    if 4@ == 20 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'PARK'  // Belleville Park
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE20' 5000 ms 1  // ~g~Destination ~w~'The Park' ~g~in Belleville
        1@ = 27.5 // floating-point values 
        2@ = -776.375 // floating-point values 
        3@ = 26.25 // floating-point values 
        7@ = 38.75 // floating-point values 
        8@ = -765.0 // floating-point values 
        9@ = 28.5625 // floating-point values
    end
end
if 03C6:   current_island == 3 
then
    0209: 4@ = random_int 21 27 
    if 4@ == 21 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'AIRPORT'  // Francis Intl. Airport
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE21' 5000 ms 1  // ~g~Destination ~w~'Francis intl. Airport'
        1@ = -744.5 // floating-point values 
        2@ = -598.5625 // floating-point values 
        3@ = 8.0 // floating-point values 
        7@ = -752.0 // floating-point values 
        8@ = -617.1875 // floating-point values 
        9@ = 15.0 // floating-point values
    end
    if 4@ == 22 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'BIG_DAM'  // Cochrane Dam
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE22' 5000 ms 1  // ~g~Destination ~w~'Cochrane Dam'
        1@ = -1111.25 // floating-point values 
        2@ = 522.875 // floating-point values 
        3@ = 65.0 // floating-point values 
        7@ = -1101.0 // floating-point values 
        8@ = 516.6875 // floating-point values 
        9@ = 70.0 // floating-point values
    end
    if 4@ == 23 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'SUB_IND'  // Pike Creek
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE23' 5000 ms 1  // ~g~Destination ~w~'import export garage' ~g~in Cochrane Dam district
        1@ = -1107.875 // floating-point values 
        2@ = 163.5 // floating-point values 
        3@ = 50.0 // floating-point values 
        7@ = -1115.375 // floating-point values 
        8@ = 155.5625 // floating-point values 
        9@ = 68.0 // floating-point values
    end
    if 4@ == 24 // integer values 
    then
        if 0121:   player $PLAYER_CHAR in_zone 'SUB_IND'  // Pike Creek
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE24' 5000 ms 1  // ~g~Destination ~w~'The hospital' ~g~in Pike Creek
        1@ = -1253.0 // floating-point values 
        2@ = -136.5625 // floating-point values 
        3@ = 55.0 // floating-point values 
        7@ = -1260.25 // floating-point values 
        8@ = -148.375 // floating-point values 
        9@ = 62.0 // floating-point values
    end
    if 4@ == 25 // integer values 
    then
        if or
        0121:   player $PLAYER_CHAR in_zone 'SUB_ZO2'  // Shoreside Vale
        0121:   player $PLAYER_CHAR in_zone 'SUB_ZO3'  // Shoreside Vale
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE25' 5000 ms 1  // ~g~Destination ~w~'The Park' ~g~in Shoreside Vale
        1@ = -230.0625 // floating-point values 
        2@ = 311.5 // floating-point values 
        3@ = 0.0 // floating-point values 
        7@ = -220.1875 // floating-point values 
        8@ = 322.5 // floating-point values 
        9@ = 10.0 // floating-point values
    end
    if 4@ == 26 // integer values 
    then
        if or
        0121:   player $PLAYER_CHAR in_zone 'PROJECT'  // Wichita Gardens
        0121:   player $PLAYER_CHAR in_zone 'SWANKS'  // Cedar Grove
        then jump @M_TAXI_GET_DESTINATION
        end
        00BC: text_highpriority 'FARE26' 5000 ms 1  // ~g~Destination ~w~'North West Towers' ~g~in Wichita Gardens
        1@ = -682.5 // floating-point values 
        2@ = 95.25 // floating-point values 
        3@ = 10.0 // floating-point values 
        7@ = -673.0 // floating-point values 
        8@ = 88.375 // floating-point values 
        9@ = 25.0 // floating-point values
    end
end
return

:M_TAXI_CALCULATE_EARNINGS
// 4@ y 5@: punto medio entre los dos puntos x-y: 1@,2@ y 7@,8@; que conforman el "cubo" (destino).
// 4@ = (1@ + 7@)/2   -   5@ = (2@ + 8@)/2
0086: 4@ = 1@ // floating-point values only 
0059: 4@ += 7@ // floating-point values 
4@ /= 2.0 // floating-point values 
0086: 5@ = 2@ // floating-point values only 
0059: 5@ += 8@ // floating-point values 
5@ /= 2.0 // floating-point values

018A: 12@ = create_checkpoint_at 4@ 5@ -100.0 
//018B: show_on_radar 12@ 2 

// 6@: distancia entre el origen (13@,14@) y el destino (4@,5@)
// 6@ = sqrt((13@ - 4@)^2 + (14@ - 5@)^2)
0086: 6@ = 13@ // floating-point values only 
0061: 6@ -= 4@ // floating-point values 
0086: 15@ = 14@ // floating-point values only 
0061: 15@ -= 5@ // floating-point values 
0069: 6@ *= 6@ // floating-point values 
0069: 15@ *= 15@ // floating-point values 
0059: 6@ += 15@ // floating-point values 
01FB: 6@ = square_root 6@ 
008C: 6@ = float_to_integer 6@ 

// 4@: permite calcular el bono de tiempo
// 5@: contiene el bono de ganancias
0084: 4@ = 6@ // integer values and handles
0084: 5@ = 6@ // integer values and handles

//0209: 6@ = random_int 10 16
0014: 5@ /= 100 // integer values 
//0068: 5@ *= 6@ // integer values 
0010: 5@ *= 12 // integer values 
return

:M_TAXI_CALCULATE_TIMER
if 03C6:   current_island == 1 
then
    if $FARES_INROW_COMPLETED == 0 // integer values 
    then 4@ *= 100 // integer values
    end
end
if 03C6:   current_island == 2 
then
    if $FARES_INROW_COMPLETED == 0 // integer values 
    then 4@ *= 95 // integer values
    end
end
if 03C6:   current_island == 3 
then
    if $FARES_INROW_COMPLETED == 0 // integer values 
    then 4@ *= 115 // integer values
    end
end
if $FARES_INROW_COMPLETED == 1 // integer values 
then 4@ *= 90 // integer values
end
if $FARES_INROW_COMPLETED == 2 // integer values 
then 4@ *= 85 // integer values
end
if $FARES_INROW_COMPLETED == 3 // integer values 
then 4@ *= 84 // integer values
end
if $FARES_INROW_COMPLETED == 4 // integer values 
then 4@ *= 83 // integer values
end
if $FARES_INROW_COMPLETED == 5 // integer values 
then 4@ *= 82 // integer values
end
if and
  $FARES_INROW_COMPLETED > 5 // integer values 
  10 >= $FARES_INROW_COMPLETED // integer values 
then 4@ *= 80 // integer values
end
if and
  $FARES_INROW_COMPLETED > 11 // integer values 
  20 >= $FARES_INROW_COMPLETED // integer values 
then 4@ *= 75 // integer values
end
if and
  $FARES_INROW_COMPLETED > 20 // integer values 
  50 >= $FARES_INROW_COMPLETED // integer values 
then 4@ *= 70 // integer values
end
if $FARES_INROW_COMPLETED > 50 // integer values 
then 4@ *= 60 // integer values
end
0058: $M_TIMER += 4@ // integer values 
if $M_TIMER_FLAG == 0 // integer values 
then
    014E: set_timer_to $M_TIMER 
    $M_TIMER_FLAG = 1 // integer values
end
4@ /= 100 // integer values 
4@ *= 65 // integer values 
17@ = 0 // integer values 
if 03C6:   current_island == 3 
then
    if $FARES_INROW_COMPLETED == 0 // integer values 
    then $M_TIMER += 15000 // integer values
    end
else
    if $FARES_INROW_COMPLETED == 0 // integer values 
    then $M_TIMER += 10000 // integer values
    end
end
return

:M_TAXI_SHOW_BONUS
if 03C6:   current_island == 3 
then
    if or
    001F:   17@ > 4@ // integer values
    $M_TAXI_MODE == 0 // No bonus
    then
        0084: $TAXI_EARNING = 5@ // integer values and handles 
        00BA: text_styled 'TAXI4' 5000 ms 5  // Fare complete!
    else
        0084: $TAXI_EARNING = 5@ // integer values and handles 
        $TAXI_EARNING *= 2 // integer values 
        00BA: text_styled 'TAXI5' 5000 ms 5  // SPEED BONUS!!
    end
else
    if or
    001F:   17@ > 4@ // integer values 
    $M_TAXI_MODE == 0 // No bonus
    then
        0084: $TAXI_EARNING = 5@ // integer values and handles 
        $TAXI_EARNING /= 2 // integer values 
        00BA: text_styled 'TAXI4' 5000 ms 5  // Fare complete!
    else
        0084: $TAXI_EARNING = 5@ // integer values and handles 
        00BA: text_styled 'TAXI5' 5000 ms 5  // SPEED BONUS!!
    end
end
return

:M_TAXI_SHOW_EARNINGS
0109: player $PLAYER_CHAR money += $TAXI_EARNING 
01E3: text_1number_styled 'TSCORE2' $TAXI_EARNING 6000 ms 6  // $~1~
018C: play_sound 94 at 0.0 0.0 0.0 
0058: $TOTAL_TAXI_EARNINGS += $TAXI_EARNING // integer values 
$TAXIMISSIONS_COMPLETED += 1 // integer values 
0315: increment_taxi_dropoffs 
$FARES_INROW_COMPLETED += 1 // integer values 
$M_TIMER += 10000 // integer values 
if and
003A:   $FARES_INROW_COMPLETED == $FARES_FOR_NEXT_INROW_BONUS // integer values and handles
$M_TAXI_MODE == 1 // With Bonus
then
    036D: text_2numbers_styled 'IN_ROW' $FARES_INROW_COMPLETED $FARES_INROW_BONUS 5000 ms 6  // ~1~ IN A ROW bonus! $~1~
    0109: player $PLAYER_CHAR money += $FARES_INROW_BONUS 
    0058: $TOTAL_TAXI_EARNINGS += $FARES_INROW_BONUS // integer values 
    $FARES_FOR_NEXT_INROW_BONUS += 5 // integer values 
    $FARES_INROW_BONUS += 8//2000 // integer values
end
return

:M_TAXI_SHOW_STATISTICS
if 001F:   $FARES_INROW_COMPLETED > $M_TAXI_RECORD // integer values 
then 0084: $M_TAXI_RECORD = $FARES_INROW_COMPLETED // integer values and handles 
end
{
Convirtiendo el dato 14@ que corresponde a la cantidad de milisegundos transcurridos durante la mision
en su equivalente en minutos:segundos
}
0014: $m_mission_time_1 /= 100 // integer values 
0084: 4@ = $m_mission_time_1 // integer values and handles 
0014: 4@ /= 600 // integer values
008D: 6@ = integer_to_float $m_mission_time_1 
0015: 6@ /= 600.0 // floating-point values 
008D: 5@ = integer_to_float 4@ 
0061: 6@ -= 5@ // floating-point values 
0011: 6@ *= 60.0 // floating-point values 
008C: 6@ = float_to_integer 6@
if 6@>9 
then 0302: text_4numbers 'M_WTX2' $FARES_INROW_COMPLETED 4@ 6@ $M_TAXI_RECORD 10000 ms 5 //Tarifas: ~1~, Tiempo de trabajo: ~1~:~1~, Record de tarifas: ~1~
else 0302: text_4numbers 'M_WTX1' $FARES_INROW_COMPLETED 4@ 6@ $M_TAXI_RECORD 10000 ms 5 //Tarifas: ~1~, Tiempo de trabajo: ~1~:0~1~, Record de tarifas: ~1~
end
$FARES_INROW_COMPLETED=0
return

:M_TAXI_JOB_EXPERIENCE
0008: $l_exp += 1 // integer values 
create_thread @DRAW_STATS ID_LABORAL_EXP
0209: 6@ = random_int 1 3 
0058: $m_taxi_exp += 6@ // integer values 
create_thread @DRAW_STATS ID_TAXI_EXP
return

{===================================================
END OF THE MISSION
===================================================}
:M_TAXI_PASSED
00BA: text_styled 'TAXI6' 5000 ms 5  // Taxi mission over
01E3: text_1number_styled 'TSCORE' $TOTAL_TAXI_EARNINGS 6000 ms 6  // EARNINGS: $~1~
0316: save_taxi_earnings_from $TOTAL_TAXI_EARNINGS 
return

:M_TAXI_FAIL
return

:M_TAXI_CLEANUP
$ONMISSION = 0 // integer values
0164: disable_marker 12@ 
01C2: remove_references_to_actor 11@ // Like turning an actor into a random pedestrian 
0151: remove_status_text $FARES_INROW_COMPLETED 
014F: stop_timer $M_TIMER 
0216: set_car 0@ taxi_available_light_to 0 
00D8: mission_cleanup 
return


