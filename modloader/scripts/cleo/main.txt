{======================================================================
======================================================================}
{$CLEO .cs}
thread 'Main'

{======================================================================
======================================================================}
const
    ICON_AIRPORT = 5
    ICON_AMMU = 6
    ICON_CJ = 15
    ICON_CRASH = 16
    ICON_ASSET_GREEN = 31
    ICON_SAVE = 35
    ICON_UNKNOWN = 37
    ICON_P_SPRAY = 63

    ACTION_INITIALIZE = 0
    ACTION_SAVE = 1
    ACTION_LOAD = 2
    ACTION_UPDATE = 3
    SAVEFILE_PATH = "cleo\2hugta.sav"
    TEMPFILE_PATH = "cleo\TEMP"
end

{======================================================================
======================================================================}
:Main
wait 0
if 0AAB:   file_exists "cleo/__2hugta__"
then
    0A9A: 0@ = openfile "cleo/__2hugta__" mode "rb"  // IF and SET
    0ADA: 1@ = scan_file 0@ format "%s" 2@s //IF and SET
    0A9B: closefile 0@
    if 1@ <> 1
    then 0ACC: show_text_lowpriority "~r~ERROR: no city information found. I don't know which city i am." time 5000
    else 0AB1: @StoreLoadedCity 2 2@ 3@
    end
    0B00: delete_file "cleo/__2hugta__"  // IF and SET
else 0A93: end_custom_thread
end
//0AB1: @GetLoadedCity 0 ret 2@ 3@

if 2@s == 'LibCity'
then 0AB1: @IIIFade 2 0 0
else fade 0 0
end

if 0AAB:   file_exists SAVEFILE_PATH
then 0A92: create_custom_thread "SaveData.s" ACTION_LOAD
else
    0A92: create_custom_thread "SaveData.s" ACTION_INITIALIZE
    gosub @InitializeDataInsideSaveFiles
end

if 2@s == 'LibCity'
then
    04CE: 4@ = create_icon_marker_without_sphere ICON_P_SPRAY at 590.2225 -353.8437 16.8069 // Portland
    04CE: 5@ = create_icon_marker_without_sphere ICON_P_SPRAY at 42.6047 -493.8029 32.1314 // Stauton
    04CE: 6@ = create_icon_marker_without_sphere ICON_P_SPRAY at -1474.9237 35.0146 64.7902 // Shoreside Vale
    
    04CE: 7@ = create_icon_marker_without_sphere ICON_AMMU at 732.2539 -395.4517 21.2086 // Portland
    04CE: 8@ = create_icon_marker_without_sphere ICON_AMMU at 16.6841 -712.3019 32.4007 // Stauton
    04CE: 9@ = create_icon_marker_without_sphere ICON_AMMU at -193.3877 174.1013 17.4917 // Shoreside Vale
    
    04CE: 10@ = create_icon_marker_without_sphere ICON_AIRPORT at -1101.3533 -600.7313 17.297
    
    016C: restart_if_wasted_at 808.9407 -597.6138 20.8587 angle 270.0 town_number 0 // Portland
    016D: restart_if_busted_at 809.0225 -674.9634 20.9303 angle 270.0 town_number 0 // Portland
    016C: restart_if_wasted_at -152.0593 -19.0699 22.1761 angle 0.0 town_number 0 // Stauton
    016D: restart_if_busted_at 5.552 -1123.3313 31.9408 angle 0.0 town_number 0 // Stauton 
    016C: restart_if_wasted_at -1585.6918 -138.7436 64.9475 angle 270.0 town_number 0 // Shoreside Vale
    016D: restart_if_busted_at -1593.1989 -45.0989 64.8514 angle 270.0 town_number 0 // Shoreside Vale

    0AB1: @InitializePosition 0
end

0247: load_model #TAXI
038B: load_requested_models
repeat
wait 0
until 0248:   model #TAXI available
00A5: 12@ = create_car #TAXI at 1073.5903 -795.7273 17.7907
0175: set_car 12@ Z_angle_to 90.0
0249: release_model #TAXI

wait 1500
if 2@s == 'LibCity'
then 0AB1: @IIIFade 2 1 2500
else fade 1 2500
end

//02A7: 21@ = create_icon_marker_and_sphere ICON_CRASH at 1091.8901 -803.3321 17.784 //1091.8901 -803.3321 17.784

while true
wait 0
    gosub @JobTriggerer
    gosub @SaveMachine
end
0A93: end_custom_thread

{======================================================================
======================================================================}
{$I common/SearchIntoTemp.txt}

{$I main/JobTriggerer.txt}
{$I main/SaveMachine.txt}

{======================================================================
======================================================================}
:IIIFadeLoop
0390: load_txd_dictionary 0@s
038F: load_texture 2@v as 1 // Load dictionary with 0390 first
038D: draw_texture 1 position 320.0 224.0 size 640.0 448.0 RGBA 255 255 255 255
0AB2: ret 0

{======================================================================
======================================================================}
:IIIFade
03F0: enable_text_draw 0
2@s='splash1'
4@v="splash1"
0A92: create_custom_thread "IIIFade.s" 0@ 1@ 2@ 3@ 4@ 5@ 6@ 7@
if 0@ == 0
then
    wait 1@
    0AB1: @IIIFadeLoop 6 2@ 3@ 4@ 5@ 6@ 7@
end
0AB2: ret 0

{======================================================================
======================================================================}
:InitializeDataInsideSaveFiles
wait 0
0A9A: 0@ = openfile "cleo\__temp_data__" mode "wb+"  // IF and SET
0AD8: write_string_to_file 0@ from "GroupId 2 0 0 0 0 0 0 0 0 0 0 0 0 0" //IF and SET
0AD8: write_string_to_file 0@ from "SelectedCharId 0 0 4 0 0 0 0 0 0 0 0 0 0 0" //IF and SET
0A9B: closefile 0@
0A92: create_custom_thread "SaveData.s" ACTION_UPDATE
return

{======================================================================
======================================================================}
:InitializePosition
const
    In_Group = 0@
    In_Char = 1@
    In_PosId = 3@
end
0AB1: @GetPlayerCharacterData 1 create_log_in_file 0 store_player_character_data_in In_Group In_Char

24@v = "StartPosId"
0AB1: @SearchIntoTemp 6 variable_to_search 24@ 25@ 26@ 27@ group In_Group create_log_in_file 0 ret 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@
0085: In_PosId = 12@(In_Char,7i) // (int)
if In_PosId == 0
then
    00A1: put_actor $PLAYER_ACTOR at 1073.3259 -807.8498 17.785 //924.7937 -1104.2604 17.8171
    0173: set_actor $PLAYER_ACTOR Z_angle_to 96.0 //40.0
end
if In_PosId == 1
then
    00A1: put_actor $PLAYER_ACTOR at 789.9366 29.2542 7.7647
    0173: set_actor $PLAYER_ACTOR Z_angle_to 275.0
    04E4: unknown_refresh_game_renderer_at 789.9366 29.2542
    03CB: set_rendering_origin_at 789.9366 29.2542 7.7647
end
0373: set_camera_directly_behind_player
02EB: restore_camera_with_jumpcut
0AB2: ret 0

{======================================================================
======================================================================}
:LoadedCity
hex
FFFFFFFF FFFFFFFF
end

{======================================================================
======================================================================}
:StoreLoadedCity
0AC6: 2@ = label @LoadedCity offset
0A8C: write_memory 2@ size 4 value 0@ virtual_protect 0
2@+=4
0A8C: write_memory 2@ size 4 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:GetLoadedCity
0AC6: 2@ = label @LoadedCity offset
0A8D: 0@ = read_memory 2@ size 4 virtual_protect 0
2@+=4
0A8D: 1@ = read_memory 2@ size 4 virtual_protect 0
0AB2: ret 2 0@ 1@
