{$CLEO .cs}
thread 'GroupManager'

{======================================================================
======================================================================}
{$I groupmanager/ModelsOffsets.txt}

{======================================================================
======================================================================}
const
    // Teams Ids
    ID_HAKUREI_SHRINE_TEAM = 0
    ID_PERFECT_CHERRY_BLOSSOM_TEAM = 1
    ID_SCARLET_DEVIL_MANSION_TEAM = 2
    ID_MOUNTAIN_OF_FAITH_TEAM = 3
    ID_BONBUNMARU_TEAM = 4
    ID_SUBTERRANEAN_ANIMISM_TEAM = 5
    ID_BAKA_TEAM = 6
    ID_FAIRY_TEAM = 7
    ID_IMPERISHABLE_NIGHT_TEAM = 8
    ID_PHANTASMAGORIA_OF_FLOWER_VIEW_TEAM = 9
    ID_SUBTERRANEAN_ANIMISM_2_TEAM = 10
    ID_UNDEFINED_FANTASTIC_OBJECT_TEAM = 11
    ID_TEN_DESIRES_TEAM = 12
    ID_CUSTOM_TEAM = 13

    Gm_State = 0@
    // 1@ 2@ 3@ 4@ 5@ 6@ 7@: 2hu Group Actresses
    Dummy1 = 8@
    Dummy2 = 9@
    Dummy3 = 10@
    Dummy4 = 11@
    TextureShown = 12@
    FlankUp = Dummy1
    FlankDown = Dummy2
    // 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@: Team in use
    AxisXPressState = 21@
    AxisX = 22@
    ThSelect = 23@
    CurrSelect = 24@
    
    FLAG_INCREASE=0
    FLAG_DECREASE=1

    FLAG_FALSE=0
    FLAG_TRUE=1

    FLAG_PAD_PRESS = 0
    FLAG_KEY_PRESS = 1
    
    PAD_LEFT_RIGHT = 0
    PAD_UP_DOWN = 1
    
    LOG_FILE_PATH = "cleo/GroupManager.log"
    SAVEFILE_PATH = "cleo\2hugta.sav"
    TEMPFILE_PATH = "cleo\TEMP"
end

{======================================================================
======================================================================}
:GroupManager_main
wait 0

if 0AAB:   file_exists SAVEFILE_PATH
then
    0AB1: @GetPlayerCharacterData 1 create_log_in_file 0 store_player_character_data_in Dummy1 ThSelect
    0085: CurrSelect = ThSelect // (int)
else
    0AB1: @Debug_ChooseGroup 0 ret Dummy1
    ThSelect=0
end

0AB1: @InitializeTeamInUse 1 Dummy1
0AB1: @GetTeamInUse 0 ret 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@
0AB1: @CopyTeamModels 0

0AB1: @ChangePlayerSkin 2 13@(ThSelect,7i) ThSelect

0A9A: Dummy1 = openfile LOG_FILE_PATH mode "wb+"
0AD9: write_formatted_text "Team in use: %.2d %.2d %.2d %.2d %.2d %.2d %.2d %.2d, selected: %d%c%c" in_file Dummy1 vars 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ ThSelect 0xd 0xa
0A9B: closefile Dummy1

TextureShown=0
while true
wait 0
    if Gm_State == 0
    then 0AB1: @ActivityMachine_GenerateCharacters 8 1@ 2@ 3@ 4@ 5@ 6@ 7@ ThSelect ret 1@ 2@ 3@ 4@ 5@ 6@ 7@
    end
    if Gm_State == 1
    then
        0A9A: Dummy1 = openfile LOG_FILE_PATH mode "ab+"
        0AD9: write_formatted_text "Team in use: %.2d %.2d %.2d %.2d %.2d %.2d %.2d %.2d, selected: %d%c%c" in_file Dummy1 vars 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ CurrSelect 0xd 0xa
        0A9B: closefile Dummy1
        fade 0 800
        wait 800
        for Dummy1 = 0 to 6
            if 056D:   actor 1@(Dummy1,7i) defined
            then 009B: destroy_actor 1@(Dummy1,7i)
            end
        end
        01B4: set_player $PLAYER_CHAR can_move 0
        0AB1: @ChangePlayerSkin 2 13@(CurrSelect,7i) CurrSelect
        0AB1: @GetTeamOffsetFromId 1 20@ ret Dummy2
        0085: Dummy1 = 13@(CurrSelect,7i) // (int)
        0062: Dummy1 -= Dummy2  // (int)     // Taking offsets to zero
        0AB1: @GetCoordinates 1 Dummy1 ret Dummy1 Dummy2 Dummy3 Dummy4
        0AB1: @MoveToInterior 5 xyz Dummy1 Dummy2 Dummy3 angle Dummy4 interior 0
        01B4: set_player $PLAYER_CHAR can_move 1
        wait 1000
        fade 1 1000
        Gm_State=0
        0085: ThSelect = CurrSelect // (int)
    end
    if 00E1:   player 0 pressed_key 4
    then
        0AB1: @LoadOrUnloadTextures 2 FLAG_TRUE TextureShown ret TextureShown
        0AB1: @EnableTabMenu 1 FLAG_TRUE
        0AB1: @StatScreen 1 CurrSelect
        0AB1: @GetButtonFlanks 3 FLAG_PAD_PRESS PAD_LEFT_RIGHT AxisXPressState ret AxisXPressState FlankUp FlankDown AxisX
        if FlankUp == 1
        then
            for Dummy1 = 0 to 6
                if 13@(Dummy1,7i) == 0xff
                then break
                end
            end
            Dummy1--
            if AxisX > 100
            then 0AB1: @IncOrDecIntVariable 4 CurrSelect 0 Dummy1 FLAG_INCREASE ret CurrSelect
            end
            if -100 > AxisX
            then 0AB1: @IncOrDecIntVariable 4 CurrSelect 0 Dummy1 FLAG_DECREASE ret CurrSelect
            end
        end
    else
        0AB1: @EnableTabMenu 1 FLAG_FALSE
        0AB1: @LoadOrUnloadTextures 2 FLAG_FALSE TextureShown ret TextureShown
        if 803B:   not ThSelect == CurrSelect  // (int)
        then Gm_State=1
        end
    end
end
0A93: end_custom_thread


{======================================================================
======================================================================}

{$I common/DrawTexts.txt}
{$I common/Math.txt}
{$I common/GetButtonFlanks.txt}
{$I common/SearchIntoTemp.txt}

{$I groupmanager/DebugChooseGroup.txt}
{$I groupmanager/ModelsList.txt}
{$I groupmanager/CopySkinFilesIntoModloaderFolder.txt}
{$I groupmanager/StatScreen.txt}

{======================================================================
======================================================================}
:TeamMembersFlag
hex
    00 01 02 03 FF FF FF
    04 05 06 07 08 FF FF
    09 0A 0B 0C 0D 0E FF
    0F 10 11 FF FF FF FF
    12 13 14 15 16 17 18
    19 1A 1B 1C FF FF FF
    1D 1E 1F 20 21 22 23
    24 25 26 27 28 29 2A
    2B 2C 2D 2E 2F 30 FF
    31 32 33 34 35 36 FF
    37 38 39 3A FF FF FF
    3B 3C 3D 3E 3F 40 41
    42 43 44 45 46 47 48
end
    
:TeamAvailabilityFlag
hex
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 01 00 00 00 00 00
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 00 01 00 00 00 00
    01 00 00 00 00 00 00
    01 01 01 00 00 00 00
    00 00 01 00 00 00 00
    01 00 00 00 00 00 00
    01 00 00 00 00 00 00
    00 00 00 00 00 00 01
    00 00 00 00 00 00 01
end
    
:TeamInUse
hex
    FF FF FF FF FF FF FF FF
end

{======================================================================
======================================================================}
:ChangePlayerSkin
0085: 2@ = 1@ // (int)
2@ += 290 // #SPECIALXX
0085: 3@ = 1@ // (int)
3@++
//0AB1: @CopyModelAndLoad 2 model_offset 0@ place_holder 3@
0AB1: @LoadModel 1 3@
09C7: change_player $PLAYER_CHAR model_to 2@
0296: unload_special_actor 3@
0AB2: ret 0

{======================================================================
======================================================================}
:ActivityMachine_GenerateCharacters
const
    At_TeamActor = 0@ // 0@ 1@ 2@ 3@ 4@ 5@ 6@
    At_Select = 7@
    At_index = 8@
    At_pX = 9@
    At_pY = 10@
    At_pZ = 11@
    At_pA = 12@
    // 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@: Team in use
    At_Dummy = 21@
    At_Dummy2 = 22@
    LONG_RADIUS = 45.0
end
0AB1: @GetTeamInUse 0 ret 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@

for At_index = 0 to 6
    if 856D:   not actor At_TeamActor(At_index,7i) defined
    then
        {16@v = "TeamInUse"
        0AB1: @SearchIntoTemp 6 variable_to_search 16@ 17@ 18@ 19@ group 0 create_log_in_file 0 ret 20@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@
        0AD3: 16@v = format "Slot%d" At_index
        0AB1: @SearchIntoTemp 6 variable_to_search 16@ 17@ 18@ 19@ group 0 create_log_in_file 0 ret 13@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@ 15@
        0085: At_Dummy = 13@ // (int)
        0AB1: @GetTeamOffsetFromId 1 20@ ret At_Dummy2
        0062: At_Dummy -= At_Dummy2  // (int)     // Taking offsets to zero}

        0AB1: @GetCoordinates 1 At_index ret At_pX At_pY At_pZ At_pA
        if 00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere At_pX At_pY At_pZ radius LONG_RADIUS LONG_RADIUS LONG_RADIUS
        then
            if and
            803B:   not At_index == At_Select  // (int)
            8039:   not 13@(At_index,8i) == 0xFF
            then
                0085: At_Dummy = At_index // (int)
                At_Dummy += 290 // #SPECIALXX
                0085: At_Dummy2 = At_index // (int)
                At_Dummy2++
                0AB1: @LoadModel 1 At_Dummy2
                gosub @DEBUG_LogCreatedActors
                009A: At_TeamActor(At_index,7i) = create_actor_pedtype 8 model At_Dummy at At_pX At_pY At_pZ
                0173: set_actor At_TeamActor(At_index,7i) Z_angle_to At_pA
                gosub @DEBUG_LogCreatedActorsDone
                0296: unload_special_actor At_Dummy2
            end
        end
    else //056D:   actor At_TeamActor(At_index,7i) defined
        if and
        8104:   not actor $PLAYER_ACTOR near_actor At_TeamActor(At_index,7i) radius LONG_RADIUS LONG_RADIUS LONG_RADIUS sphere 0
        80FE:   not actor $PLAYER_ACTOR sphere 0 in_sphere At_pX At_pY At_pZ radius LONG_RADIUS LONG_RADIUS LONG_RADIUS
        then
            //gosub @GET_CLOSEST_LOCATION_TEAM_ACTOR // modifies 19@(0@,7i) and 16@ 17@ 18@ with 19@ information
            009B: destroy_actor At_TeamActor(At_index,7i)
        end
    end
end
0AB2: ret 7 0@ 1@ 2@ 3@ 4@ 5@ 6@

:DEBUG_LogCreatedActors
0AB1: @DEBUG_GetModelName 1 13@(At_index,8i) ret 25@ 26@
0A9A: 30@ = openfile LOG_FILE_PATH mode "ab+"
0AD9: write_formatted_text "creating... idx: %d, skin: %s, offset: %d, model: %d, as: %d (SP%.2d.dff)" in_file 30@ vars At_index 25@s 13@(At_index,8i) At_Dummy At_Dummy2 At_Dummy2
0AD9: write_formatted_text ", xyz %f %f %f angle %f" in_file 30@ vars At_pX At_pY At_pZ At_pA
0A9B: closefile 30@
return

:DEBUG_LogCreatedActorsDone
0A9A: 30@ = openfile LOG_FILE_PATH mode "ab+"
0AD9: write_formatted_text " ...done%c%c" in_file 30@ vars 0xd 0xa
0A9B: closefile 30@
return

{======================================================================
======================================================================}
:GetCoordinates
0AB1: @Set4Numbers 4 2574.6206 -969.0294 81.6224 4.0 ret 1@ 8@ 15@ 22@
0AB1: @Set4Numbers 4 2578.0574 -967.3101 81.3602 63.0 ret 2@ 9@ 16@ 23@
0AB1: @Set4Numbers 4 2572.8013 -968.2211 81.8495 158.0 ret 3@ 10@ 17@ 24@
0AB1: @Set4Numbers 4 2576.2988 -968.3019 81.4886 273.0 ret 4@ 11@ 18@ 25@
0AB1: @Set4Numbers 4 2572.0461 -964.0752 82.0216 255.0 ret 5@ 12@ 19@ 26@
0AB1: @Set4Numbers 4 2575.9158 -961.5757 81.3516 268.0 ret 6@ 13@ 20@ 27@
0AB1: @Set4Numbers 4 2582.4094 -962.0638 81.3516 112.0 ret 7@ 14@ 21@ 28@
0AB2: ret 4 1@(0@,7f) 8@(0@,7f) 15@(0@,7f) 22@(0@,7f)

{======================================================================
======================================================================}
:InitializeTeamInUse
const
    It_TeamId = 0@
    It_Index = 1@
    It_Addr = 2@
    It_Value = 3@
end
for It_Index = 0 to 6
    0AB1: @GetTeamModels 2 team_id It_TeamId member_id It_Index ret It_Value
    0AB1: @InsertValueInTeamInUse 2 offset It_Index value It_Value
end
0AB1: @InsertValueInTeamInUse 2 offset 7 value It_TeamId
0AB2: ret 0

{======================================================================
======================================================================}
:GetTeamOffsetFromId
1@=OFFS_HAKUREI_SHRINE_TEAM
2@=OFFS_PERFECT_CHERRY_BLOSSOM_TEAM
3@=OFFS_SCARLET_DEVIL_MANSION_TEAM
4@=OFFS_MOUNTAIN_OF_FAITH_TEAM
5@=OFFS_BONBUNMARU_TEAM
6@=OFFS_SUBTERRANEAN_ANIMISM_TEAM
7@=OFFS_BAKA_TEAM
8@=OFFS_FAIRY_TEAM
9@=OFFS_IMPERISHABLE_NIGHT_TEAM
10@=OFFS_PHANTASMAGORIA_OF_FLOWER_VIEW_TEAM
11@=OFFS_SUBTERRANEAN_ANIMISM_2_TEAM
12@=OFFS_UNDEFINED_FANTASTIC_OBJECT_TEAM
13@=OFFS_TEN_DESIRES_TEAM
14@=OFFS_CUSTOM_TEAM
0AB2: ret 1 1@(0@,14i)

{======================================================================
======================================================================}
:InsertValueInTeamInUse
0AC6: 2@ = label @TeamInUse offset
005A: 2@ += 0@  // (int)
0A8C: write_memory 2@ size 1 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:GetTeamInUse
0AC6: 9@ = label @TeamInUse offset
for 8@ = 0 to 7
    0AC6: 9@ = label @TeamInUse offset
    005A: 9@ += 8@  // (int)
    0A8D: 0@(8@,8i) = read_memory 9@ size 1 virtual_protect 0
end
0AB2: ret 8 0@ 1@ 2@ 3@ 4@ 5@ 6@ 7@

{======================================================================
======================================================================}
:StoreTeamInUse
// inputs: 0@ 1@ 2@ 3@ 4@ 5@ 6@ 7@
for 8@ = 0 to 7
    0AC6: 9@ = label @TeamInUse offset
    005A: 9@ += 8@  // (int)
    0A8C: write_memory 9@ size 1 value 0@(8@,8i) virtual_protect 0
end
0AB2: ret 0

{======================================================================
======================================================================}
:GetTeamModels
const
    Me_TeamId = 0@
    Me_MemberId = 1@
    Me_Addr = 2@
    Me_Offset = 3@
    Me_Value = 4@
end
0AC6: Me_Addr = label @TeamMembersFlag offset
0085: Me_Offset = Me_TeamId // (int)
0012: Me_Offset *= 7
005A: Me_Offset += Me_MemberId  // (int)
005A: Me_Addr += Me_Offset  // (int)
0A8D: Me_Value = read_memory Me_Addr size 1 virtual_protect 0
0AB2: ret 1 Me_Value

{======================================================================
======================================================================}
:GetFromArray
// inputs: 0@: array address, 1@: row, 2@: element
const
    Ga_Addr = 0@
    Ga_Row = 1@
    Ga_Elem = 2@
    Ga_Offset = 3@
    Ga_Pointer = 4@
    Ga_Value = 5@
end
0AC6: Ga_Pointer = label Ga_Addr offset
0085: Ga_Offset = Ga_Row // (int)
0012: Ga_Offset *= 7
005A: Ga_Offset += Ga_Elem  // (int)
005A: Ga_Pointer += Ga_Offset  // (int)
0A8D: Ga_Value = read_memory Ga_Pointer size 1 virtual_protect 0
0AB2: ret 1 Ga_Value

{======================================================================
======================================================================}
:ActivityMachine_ChangePlayer
0AB2: ret 0

{======================================================================
======================================================================}
:MoveToInterior
// 0@ 1@ 2@ 3@: xyz-angle, 4@: interior
select_interior 4@
0860: link_actor $PLAYER_ACTOR to_interior 4@
04E4: unknown_refresh_game_renderer_at 0@ 1@
03CB: set_rendering_origin_at 0@ 1@ 2@
00A1: put_actor $PLAYER_ACTOR at 0@ 1@ 2@
0173: set_actor $PLAYER_ACTOR Z_angle_to 3@
0373: set_camera_directly_behind_player
02EB: restore_camera_with_jumpcut
0AB2: ret 0

{======================================================================
======================================================================}
:EnableTabMenu
if 0@ == 1
then 1@ = 0.0
    2@ = 0
else 1@ = 1.0
    2@ = 1
end
0AB1: @GetInt 1 @TabMenuEnabled 10@
if and
0@ == 1
10@ == 0
then 20@=1
end
if and
0@ == 0
10@ == 1
then 20@=1
end
if 20@==1
then
    015D: set_gamespeed 1@
    0581: enable_radar 2@
    0826: enable_hud 2@
    01B4: set_player $PLAYER_CHAR can_move 2@
    03F0: enable_text_draw 0@
    09BA: show_entered_zone_name 2@
end
0AB1: @StoreInt 2 @TabMenuEnabled 0@
0AB2: ret 0

:TabMenuEnabled
hex
00
end

:GetInt
// 0@: label, 1@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 1@  // (int)
0A8D: 11@ = read_memory 10@ size 1 virtual_protect 0
0AB2: ret 1 11@

:StoreInt
// 0@: label, 1@: value, 2@: offset for address
0AC6: 10@ = label 0@ offset
005A: 10@ += 2@  // (int)
0A8C: write_memory 10@ size 1 value 1@ virtual_protect 0
0AB2: ret 0

{======================================================================
======================================================================}
:LoadOrUnloadTextures
// inputs: 0@: load or unload flag, 1@: Texture shown
const
    Lut_Flag = 0@
    Lut_TxdShown = 1@
    Lut_Index = 2@
    Lut_Aux = 3@
    Lut_LongStr = 4@v // 4@ 5@ 6@ 7@
end
0AB1: @GetTeamInUse 0 ret 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@
if Lut_Flag == 1
then
    if Lut_TxdShown == 0
    then
        for Lut_Index = 1 to 8
            0AD3: Lut_LongStr = format "bar%d" Lut_Index
            0390: load_txd_dictionary Lut_LongStr
            0085: Lut_Aux = Lut_Index // (int)
            Lut_Aux += 100
            038F: load_texture Lut_LongStr as Lut_Aux // Load dictionary with 0390 first
        end
        for Lut_Index = 0 to 6
            if 13@(Lut_Index,7i) <> 255
            then
                0AD3: Lut_LongStr = format "TH_%.2d" 13@(Lut_Index,7i)
                0390: load_txd_dictionary Lut_LongStr
                if 13@(Lut_Index,7i) == 0
                then 038F: load_texture Lut_LongStr as 100 // Load dictionary with 0390 first
                else 038F: load_texture Lut_LongStr as 13@(Lut_Index,7i) // Load dictionary with 0390 first
                end
            end
        end
        Lut_TxdShown = 1
    end
else
    if Lut_TxdShown == 1
    then
        wait 0
        Lut_TxdShown = 0
        0391: release_textures
    end
end
0AB2: ret 1 Lut_TxdShown

